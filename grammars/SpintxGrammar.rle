MAPPING-PREFIX = @ ;
DELIMITERS = "<$.>" "<$?>" "<$!>" "<$:>";

## -- #LATEX T: [Espacio para la etiqueta que asigna la regla.]
## -- #LATEX E: [Espacio para incluir \emph{un ejemplo} con la parte relevante en cursiva.] 
## -- #LATEX D: [Espacio para describir con palabras lo que hace la regla.]

### Comment this to avoid problems with semicolon now
### DELIMITERS = "<$.>" "<$?>" "<$!>" "<$:>" "<$\;>" ;

#LATEX\chapter*{Introducción}
#LATEXDOCU Este documento describe el proceso de anotación de las transcripciones de los clips del corpus SPinTX (Spanish in Texas) con información pedagógica para facilitar el uso de los mismos en la enseñanza y el aprendizaje de español como lengua extranjera.

#LATEXDOCU 

#LATEX\part{Definición de LIST y SET}
#LATEX\chapter{LIST y SET basados en información morfosintáctica}
#LATEX\section{Categorías gramaticales}

## LISTS OR SETS FOR PART-OF-SPEECH TAGS
LIST ArtPronDemo = (DM);
LIST Nombre = (Noun);
LIST Adv = (ADV);
LIST Prep = (Preposition) (PREP);
LIST FiniteVerbForms = (VLfin) (VEfin) (VHfin) ;
LIST FiniteFormsInPast = (VLfin Indi Pret) (VEfin Indi Pret) ;
LIST PretImperfSimple = (Imp Indi) ;
LIST PretPerfSimple = (Pret Indi) ;
LIST Adj = (ADJ) (ORD) ;
LIST Part = (VLadj) (VSadj);
LIST Verbo = (Verb);
LIST Infinitive = (VLinf) (VEinf) (VHinf) (VSinf);
LIST Gerundio = (VLger) (VEger) (VHger) (VSger);
LIST PronombrePersonal = (PPX);
LIST DetAll = (ART); # habrá que refinar la definición 
LIST Sustantivo = (NC) (NP) (NMEA) (NMON) ;
LIST Articulo = (ART);
LIST Conjuncion = (Conjunction);
SET AdjOrPart = Adj OR Part ;
SET FiniteVerbFormsOrPart = FiniteVerbForms OR Part ;
LIST PronPers = (PPX);
LIST PronPersClitico = (PPC);
LIST PronPersSE = (SE);
LIST PalabraEnIngles = (lang:en);
LIST PalabraEnEspanol = (lang:es);
LIST Pasado = (Past); #only for subjunctive (?)
LIST Presente = (Pres);

LIST Subjuntivo = (Subj);

LIST LimiteOracionSimple = (CC) (CSUBX) (CSUBF) (CQUE) (CSUBI) ; # habrá que refinar la definición
SET GrupoNominal = Sustantivo OR Adj OR DetAll; # habrá que refinar la definición de grupo nominal

#LATEX\section{Rasgos morfosintácticos}
## LIST MORPH FEATURES VERBS

LIST PL = (Plural);
LIST SG = (Sing) (Singular);
LIST 1aPers = (1st);
LIST 3aPers = (3rd);
LIST 1aO3aPersAmbig = (1st_or_3rd);

SET 3aPersAmbig = 3aPers OR 1aO3aPersAmbig;

#LATEX\chapter{LIST y SET basados en lemas}
#LATEX\section{Los LIST}
## LISTS OR SETS FOR LEMMATA
LIST APrep = ("a");
LIST Pesar = ("pesar");

LIST Por = ("por");
LIST Para = ("para");
LIST Haber = ("haber") ;
LIST Personas = ("niño") ("mujer") ("hombre") ("chico") ("viejo") ("mayor") ("hijo") ("estudiante") ("muchacho") ("persona");
LIST Ser = ("ser");
LIST Servir = ("servir");
LIST Bueno = ("bueno");
LIST Malo = ("malo");
LIST No = ("no");
LIST Gustar-Like = ("gustar") ("interesar") ("fascinar") ("encantar");
LIST Sorprender-Like = ("sorprender");
LIST Yo = ("yo" PPX);
LIST Tu = ("tú" PPX);
LIST Nos = ("nosotros" PPX);
LIST Os = ("vosotros" PPX);
LIST LeLema = ("él" PPC);
LIST PretPerfRelations = (r:PretPerifAux) (r:PretPerifMain);
LIST Que = ("que");
LIST Uno = ("uno");
LIST UstedLema = ("usted");
LIST ConjLetraO = ("o");
LIST MarcDiscRevision = ("pero") ("aunque") ("exepto");
LIST Sin = ("sin");
LIST Embargo = ("embargo");
LIST De = ("de");
LIST Hecho = ("hecho") ("<hecho>");
LIST En = ("en");
LIST Realidad = ("realidad");
LIST Cierto = ("cierto");
LIST Debido =("deber|debido");
LIST Porque =("porque");
LIST Gracia =("gracia"); #now used to handle "gracias" which does not have a lemmatisation of its own, which is probably ok
LIST Dios =("dios");
LIST SoloAcento =("sólo");
LIST ElLema = ("el");
LIST VerboSer = ("ser");
LIST VerboHablar = ("hablar");
LIST VerboSerAmbiguoConErar = ("erar|ser"); ## eras, eran, era, eres (no éramos).
LIST VerboSerAmbiguoConIr = ("ser|ir"); ## fui, fuimos, fue, fuiste...
LIST VerboEstar = ("estar");
LIST ListaVerbosCorpusParaContactoMixtos = ("ser") ("decir") ("hacer") ("hacerse");
LIST ConLema = ("con");
LIST FormasDePalabrasEnLocucionesConPor = ("<ello>") ("<eso>");
LIST LemasDePalabrasEnLocucionesConPor = ("ejemplo") ("tanto") ("ello") ("eso");
LIST LemasDePalabrasEnLocucionesConPorLo = ("regular") ("tanto") ("pronto");
LIST Tanto = ("tanto");
LIST FormasDePalabrasEnLocucionesCausalesConPor = ("<ello>") ("<eso>");
LIST Ejemplo = ("ejemplo");
LIST Ademas = ("además");
LIST Tambien = ("también");
LIST Vez = ("vez");
LIST Tal = ("tal");
LIST EsteEseAquel = ("ese") ("este") ("aquel");

##de momento no se usa
LIST VerboSaberAmbiguoConSer = ("saber|ser");

#LATEX\section{Los SET}
SET BuenoOMalo = Bueno OR Malo;
SET PorOPara = Por OR Para;
SET SerOServir = Ser OR Servir;

#LATEX\chapter{LIST y SET basados en palabras (literales)}
LIST YoLiteral = ("<yo>") ("<Yo>") ("<YO>") ;
LIST TuLiteral = ("<tú>") ("<Tú>") ("<TÚ>") ;
LIST ElOEllaLiterales = ("<él>") ("<Él>") ("<ÉL>") ("<ella>") ("<Ella>") ("<ELLA>") ;
LIST NosotrosLiteral = ("<nosotros>") ("<Nosotros>") ("<NOSOTROS>") ;
LIST VosotrosLiteral = ("<vosotros>") ("<Vosotros>") ("<VOSOTROS>") ;
LIST EllosOEllasLiteral = ("<ellos>") ("<Ellos>") ("<ELLOS>") ("<ellas>") ("<Ellas>") ("<ELLAS>") ;
LIST Sea = ("<sea>") ("<SEA>") ("<Sea>");
LIST LoLiteral = ("<lo>") ("<LO>") ("<Lo>");
LIST Me = ("<me>") ("<ME>") ("<Me>");
LIST SeLiteral = ("<se>") ("<SE>") ("<Se>");
LIST NosLiteral = ("<nos>") ("<NOS>") ("<Nos>");
LIST Te = ("<te>") ("<TE>") ("<Te>");
LIST OsLiteral = ("<os>") ("<OS>") ("<Os>");
LIST EsteLiteral = ("<este>") ("<ESTE>") ("<Este>");
LIST EstoLiteral = ("<esto>") ("<ESTO>") ("<Esto>");
LIST EsoLiteral = ("<eso>") ("<ESO>") ("<Eso>");
LIST AquelloLiteral = ("<aquello>") ("<AQUELLO>") ("<Aquello>");
LIST EranLiteral = ("<eran>") ("<Eran>") ("<ERAN>");
LIST ErasLiteral = ("<eras>") ("<Eras>") ("<ERAS>");
LIST LosLiteral = ("<los>") ("<Los>") ("<LOS>");
LIST ElDetLiteral = ("<el>") ("<El>") ("<EL>");
LIST LaLiteral = ("<la>") ("<LA>") ("<La>");
LIST LasLiteral = ("<las>") ("<LAS>") ("<Las>");

LIST MiSingLiteral = ("<mi>") ("<MI>") ("<Mi>");
LIST MiPluLiteral = ("<mis>") ("<MIS>") ("<Mis>");
LIST TuSingLiteral = ("<tu>") ("<TU>") ("<Tu>");
LIST TuPluLiteral = ("<tus>") ("<TUS>") ("<Tus>");
LIST SuSingLiteral = ("<su>") ("<SU>") ("<Su>");
LIST SuPluLiteral = ("<sus>") ("<SUS>") ("<Sus>");
LIST NuestroSingLiteral = ("<nuestro>") ("<NUESTRO>") ("<Nuestro>");
LIST NuestroPluLiteral = ("<nuestros>") ("<NUESTROS>") ("<Nuestros>");
LIST VuestroSingLiteral = ("<vuestro>") ("<VUESTRO>") ("<Vuestro>");
LIST VuestroPluLiteral = ("<vuestros>") ("<VUESTROS>") ("<Vuestros>");
LIST NuestraSingLiteral = ("<nuestra>") ("<NUESTRA>") ("<Nuestra>");
LIST NuestraPluLiteral = ("<nuestras>") ("<NUESTRAS>") ("<Nuestras>");
LIST VuestraSingLiteral = ("<vuestra>") ("<VUESTRA>") ("<Vuestra>");
LIST VuestraPluLiteral = ("<vuestras>") ("<VUESTRAS>") ("<Vuestras>");

SET DetPosSing = MiSingLiteral OR TuSingLiteral OR SuSingLiteral;
SET DetPosPlu = MiPluLiteral OR TuPluLiteral OR SuPluLiteral;
SET DetPosFemPlu = NuestraPluLiteral OR VuestraPluLiteral;
SET DetPosMascPlu =  NuestroPluLiteral OR VuestroPluLiteral;
SET DetPosFemSing = NuestraPluLiteral OR VuestraPluLiteral;
SET DetPosMascSing =  NuestroPluLiteral OR VuestroPluLiteral;

LIST ElLiteralMayusc = ("<El>");
LIST PasoLiteralMayusc = ("<Paso>");
LIST EstadosLiteralMayusc = ("<Estados>");
LIST UnidosLiteralMayusc = ("<Unidos>");

LIST Todos = ("<todos>") ("<TODOS>") ("<Todos>");
LIST Modos = ("<modos>") ("<MODOS>") ("<Modos>");
LIST Es = ("<es>") ("<Es>") ("<ES>");

SET DetArtDefinidosLiterales = ElDetLiteral OR LosLiteral OR LasLiteral OR LaLiteral;
SET PronPersFuerte = YoLiteral OR TuLiteral OR ElOEllaLiterales OR NosotrosLiteral OR VosotrosLiteral OR EllosOEllasLiteral OR UstedLema;
SET PronombreDeObjetoNoAmbiguos = Me OR Te OR NosLiteral OR OsLiteral;
SET EstoEsoAquello = EstoLiteral OR EsoLiteral OR AquelloLiteral;

#LATEX\chapter{LIST y SET basados en etiquetas pedagógicas}
## PEDAGOGICAL TAGS
LIST GramPrep = (@Gram:Prep:Por) (@Gram:Prep:Por:Locuciones) (@Gram:Prep:Para) (@Gram:Prep:Para:Opinion) (@Gram:Prep:Para:Destinatario) (@Gram:Prep:Para:Habilidad);
LIST GramPron = (@Gram:Pron:Otros) (@Gram:Pron:ObjetoDoble);

#LATEX\part{Secciones previas}
BEFORE-SECTIONS

SUBSTITUTE ("por" Adverb ADV) ("por" Preposition PREP) TARGET ("por");

#LATEX\chapter{Reglas para rectificar errores del análisis morfosintático}
SUBSTITUTE (Noun NC Singular Masculine) (Conjunction CC) TARGET ("pero");
#LATEX T: CC
#LATEX E: \emph{pero} 
#LATEX D: Regla para cambiar a \emph{pero} la lectura de nombre por la de conjunción. Ahora se aplica indiscriminadamente, pero podría ser conveniente controlar que antes del pero no hay un artículo. Por ejemplo: ``el pero de la cuestión es que podría romperse el motor''.

SUBSTITUTE ("hecho" Noun NC Singular Masculine) ("hacer" Verb VLadj) TARGET ("hecho") IF (-1 Haber + Verbo);
#LATEX T: Cambia Noun NC Singular Masculine por Verb VLadj
#LATEX E: \emph{hecho} 
#LATEX D: Regla para cambiar a \emph{pero} la lectura de nombre por la de conjunción. Ahora se aplica indiscriminadamente, pero podría ser conveniente controlar que antes del pero no hay un artículo. Por ejemplo: ``el pero de la cuestión es que podría romperse el motor''.

SUBSTITUTE ("ranchar" Verb VLfin Pres Indi Sing 1st) ("rancho" Noun NC Singular Masculine) TARGET ("ranchar");
#LATEX T: Cambia Verb VLfin Pres Indi Sing 1st por Noun NC Singular Masculine en la palabra ``rancho''.
#LATEX E: \emph{rancho} 
#LATEX D: Lo hace de forma indiscriminada. Quizá en el futuro habrá que cambiarla, refinarla.

SUBSTITUTE ("dicho" Determiner QU) ("decir" Verbo VLadj) TARGET ("dicho");
#LATEX T: Cambia lectura de ``dicho'' como determinante por lectura como adjetivo/participio.
#LATEX E: \emph{dicho} 
#LATEX D: Lo hace de forma indiscriminada. Quizá en el futuro habrá que cambiarla, refinarla.

SUBSTITUTE ("oír" Verb VLfin Pres Subj Sing 1st_or_3rd) ("oír" Verb VLfin Pres Indi_Command Sing 2nd_3rd) TARGET ("oír");
#LATEX T: Cambia lectura de "oye" como presente de subjuntivo por lectura de presente de indicativo/imperativo.
#LATEX E: \emph{oye} 
#LATEX D: Lo hace de forma indiscriminada. Quizá en el futuro habrá que cambiarla, refinarla.

SUBSTITUTE ("ser" Verb VSfin Pret Indi Plural 3rd) ("ir" Verb VLfin Pret Indi Plural 3rd) TARGET ("<fueron>") IF (-1C SeLiteral);
SUBSTITUTE ("ser" Verb VSfin Pret Indi Plural 3rd) ("ir" Verb VLfin Pret Indi Plural 3rd) TARGET ("<fueron>") IF (1 ConLema + Prep);
SUBSTITUTE ("ser" Verb VSfin Pret Indi Plural 3rd) ("ir" Verb VLfin Pret Indi Plural 3rd) TARGET ("<fueron>") IF (1 Gerundio);
#LATEX T: Reglas que cambian la lectura de "ser" por "ir" en "fueron" cuando no va precedida de un pronombre se o seguida la preposición con o un gerundio.
#LATEX E: \emph{se fueron juntos, se fueron a la escuela} 
#LATEX D: Quizá en el futuro habrá que cambiarlas, refinarlas.

SUBSTITUTE ("maestro" Adjective ADJ Singular) ("maestro" Noun Singular) TARGET ("maestro");
SUBSTITUTE ("maestro" Adjective ADJ Plural) ("maestro" Noun Plural) TARGET ("maestro");
#LATEX T: Cambia lectura de "maestro" de adjetivo sustantivo.
#LATEX E: \emph{maestro, maestra, maestros, maestras} 
#LATEX D: Lo hace de forma indiscriminada. Quizá en el futuro habrá que cambiarla, refinarla. Por ejemplo, en "obras maestras", que no ocurre en la versión actal del corpus.

SUBSTITUTE ("erar|ser" Verb VLfin Past Subj Sing 2nd) ("ser" Verb VSfin Past Indi Sing 2nd) TARGET ErasLiteral; #eras
SUBSTITUTE ("erar|ser" Verb VLfin Past Subj Plural 2nd_or_3rd) ("ser" Verb VSfin Past Indi Plural 3rd) TARGET EranLiteral; #eran
#LATEX T: Cambia lectura de "eras" y "eran" de lema ambiguo entre erar y ser a sólo lema ser.
#LATEX E: \emph{eras, eran} 
#LATEX D: Consideramos erar un verbo muy poco frecuente. Si hiciera falta, se podría refinar la regla.

#LATEX\part{Secciones principales}

SECTION
#LATEXDOCU Las reglas de identificación de fenómenmos pedagógicos están ordenadas de más concretas a más generales. Así, por ejemplo, se detectan primero, cuando es posible, los distintos usos de \emph{para} (opinión, intención, destino, etc. como \emph{@Gram:Prep:Para:Opinion}...) y luego se marcan como simplemente \emph{@Gram:Prep:Para} el resto de ocurrencias de \emph{para}. Luego, se hace lo propio con otras preposiciones, hasta el punto que si para una preposición no hay reglas especiales, sus ocurrencias se marcan simplemente como \emph{@Gram:Prep}.

#LATEXDOCU Esta estrategia se utiliza para evitar que una misma palabra reciba por partida doble informaciones de igual granularidad. Para ello, antes de asignar una etiqueta, se comprueba que no haya otra asignada del mismo nivel. A efectos prácticos, etiquetas como \emph{@Gram:Prep:Para:Opinion}...) y \emph{@Gram:Prep:Para} se consideran del mismo nivel.

#LATEXDOCU Para evitar este tipo de duplicidades también se podría usar una estrategia basada en la distinción entre ADD y MAP (véase la documentación de vislcg3). Por ahora no se ha hecho, porque esto impide la asignación de múltiples etiquetas a un mismo token/una misma palabra.

#LATEXDOCU NOTA: Ahora mismo sólo estamos asignando hasta tres niveles de descripción pedagógica. Veáse el Google Doc Pedagogical tipologies \url{https://docs.google.com/spreadsheet/ccc?key=0AlyBsKogCRnwdDd3ZWRjejZlQU5XZWl6WVVUZmtKcEE&usp=sharing}.

#LATEX\chapter{Uso de los determinantes}
## SECTION -- RULES TO ADD Determiner-RELATED INFORMTATION
#LATEX\section{Usos de los artículos definidos}
#LATEXDOCU Identificaremos los usos de los artículos definidos \emph{el, la, los, las}, y el artículo (en algunas gramáticas considerado pronombre) \emph{lo} cuando va seguido de un adjetivo.

ADD (@Gram:Det:Art) Articulo IF (0 DetArtDefinidosLiterales) (NOT 1 FiniteVerbForms OR PronombreDeObjetoNoAmbiguos OR SeLiteral);
#LATEX T: Asigna @Gram:Det:Art
#LATEX E: \emph{el, la, los, las}
#LATEX D: Cualquier ocurrencia no seguida de un verbo finito o un pronombre clítico, porque treetagger no distingue entre su lectura como determinante y la pronominal.

ADD (@Gram:Det:Art) Articulo IF (0 LoLiteral) (*1 Adj BARRIER FiniteVerbForms OR Conjuncion OR LimiteOracionSimple) ;
#LATEX T: Asigna @Gram:Det:Art
#LATEX E: \emph{lo} + ADJETIVO
#LATEX D: Cualquier ocurrencia de LO seguida de un adjetivo (con un adv opcional en medio).

#LATEX\section{Usos de los determinantes demostrativos}

ADD (@Gram:Det:Demo) ArtPronDemo IF (-1 Prep OR FiniteVerbForms) (0 EsteEseAquel) (NOT 0 EstoEsoAquello) (1 Nombre) ;
#LATEX T: Asigna @Gram:Det:Demo
#LATEX E: Prep/Verbo \emph{este/ese/aquel} (LEMMA) + Nombre
#LATEX D: Cualquier ocurrencia de un demostrativo precedida de una preposición o un verbo conjugado y seguido de un nombre.

#LATEX\section{Usos de los determinantes posesivos}

ADD (@Gram:Det:Pos) DetPosSing IF (*1 Nombre + SG BARRIER FiniteVerbForms OR Conjuncion OR LimiteOracionSimple) ;
ADD (@Gram:Det:Pos) DetPosPlu IF (*1 Nombre + PL BARRIER FiniteVerbForms OR Conjuncion OR LimiteOracionSimple) ;
ADD (@Gram:Det:Pos) DetPosFemPlu IF (*1 Nombre + PL BARRIER FiniteVerbForms OR Conjuncion OR LimiteOracionSimple) ;
ADD (@Gram:Det:Pos) DetPosMascPlu IF (*1 Nombre + PL BARRIER FiniteVerbForms OR Conjuncion OR LimiteOracionSimple) ;
ADD (@Gram:Det:Pos) DetPosFemSing IF (*1 Nombre + SG BARRIER FiniteVerbForms OR Conjuncion OR LimiteOracionSimple) ;
ADD (@Gram:Det:Pos) DetPosMascSing IF (*1 Nombre + SG BARRIER FiniteVerbForms OR Conjuncion OR LimiteOracionSimple) ;
#LATEX T: Asigna @Gram:Det:Poss
#LATEX E: Prep/Verbo \emph{mi/tu/su/sus/nuestro/nuestros/vuestro/vuestros} (LEMMA) + Nombre
#LATEX D: Cualquier ocurrencia de un determinante posesivo seguido de un nombre (se admiten ADV y ADJ intercalados).

#LATEX\chapter{Uso de las preposiciones}
## SECTION -- RULES TO ADD Prep-RELATED INFORMTATION

#LATEX\section{Usos de para}
#LATEXDOCU Veáse \url{http://dev.coerll.utexas.edu/spanishtx/spe/html/adv11.html}.

#LATEX\subsection{El uso de \emph{para} para expresar el destinatario o audiencia de una acción}
##ADD (@Gram:Prep:Para:Destinatario) Prep IF (-1 SerOServir + Verb) (0 Para) (NOT 0 GramPrep) (1 DetAll) (2 Personas);
ADD (@Gram:Prep:Para) Prep IF (-1 SerOServir + Verbo) (0 Para) (NOT 0 GramPrep) (1 DetAll) (2 Personas);
#LATEX T: Asigna @Gram:Prep:Para
#LATEX E: Fue escrito \emph{para} los estudiantes.
#LATEX E: Es fundamental \emph{para} una persona.
#LATEX D: Requiere \emph{para + DET + NOMBRE $<+humano>$}

##ADD (@Gram:Prep:Para:Destinatario) Prep IF (-1 SerOServir + Verb) (0 Para) (NOT 0 GramPrep) (1 Personas);
ADD (@Gram:Prep:Para) Prep IF (-1 SerOServir + Verbo) (0 Para) (NOT 0 GramPrep) (1 Personas);
#LATEX T: Asigna @Gram:Prep:Para.
#LATEX E: Ropa \emph{para} niños.
#LATEX D: Requiere \emph{para + NOMBRE $<+humano>$}

#LATEX\subsection{El uso de \emph{para} para expresar una finalidad}
## ADD (@Gram:Prep:Para:Finalidad) Prep IF (-1 BuenoOMalo) (0 Para) (NOT 0 GramPrep) (1 Infinitive);
#LATEXREM Dudo de si el uso de ``Juan es muy bueno para el fútbol'' se parece o no al de ``hemos venido antes para no quedarnos sin asiento''. De momento los pongo juntos.

###ADD (@Gram:Prep:Para:Finalidad) Prep IF (0 Para) (NOT 0 GramPrep) (1 Infinitive);
ADD (@Gram:Prep:Para) Prep IF (0 Para) (NOT 0 GramPrep) (1 Infinitive);
#LATEX T: Asigna @Gram:Prep:Para.
#LATEX E: Soy muy mala \emph{para} recordar.
#LATEX D: Requiere \emph{para + INFINITIVO}

## ADD (@Gram:Prep:Para:Finalidad) Prep IF (0 Para) (NOT 0 GramPrep) (1 PronPersFuerte OR No) (2 Infinitive);
ADD (@Gram:Prep:Para) Prep IF (0 Para) (NOT 0 GramPrep) (1 PronPersFuerte OR No) (2 Infinitive);
#LATEX T: Asigna @Gram:Prep:Para.
#LATEX E: Había que saber hablar inglés \emph{para} uno desarrollar su trabajo.
#LATEX D: Requiere \emph{para + PRONOMBRE PERSONAL NOMINATIVO + INFINITIVO}

## ADD (@Gram:Prep:Para:Finalidad) Prep IF (0 Para) (NOT 0 GramPrep) (1 Que);
ADD (@Gram:Prep:Para) Prep IF (0 Para) (NOT 0 GramPrep) (1 Que);
#LATEX T: Asigna @Gram:Prep:Para.
#LATEX E: Me daba clases \emph{para} que aprendiera inglés.
#LATEX D: Requiere \emph{para + QUE}

#LATEX\subsection{El uso de \emph{para} para expresar una opinión}
## ADD (@Gram:Prep:Para) Prep IF (0 Para) (NOT 0 GramPrep) (1 PronombrePersonal) (NOT 1 YoLiteral OR TuLiteral);
ADD (@Gram:Prep:Para) Prep IF (0 Para) (NOT 0 GramPrep) (1 PronombrePersonal) (NOT 1 YoLiteral OR TuLiteral);
#LATEX T: Asigna @Gram:Prep:Para.
#LATEX E: \emph{Para} mi.
#LATEX D: Requiere \emph{para + PRONOMBRE PERSONAL ACUSATIVO}
#LATEX C: Esta regla es muy floja. Habrá que revisarla.

#LATEX\subsection{Usos de \emph{para} no clasificados}
ADD (@Gram:Prep:Para) Prep IF (0 Para) (NOT 0 GramPrep);
#LATEX T: Asigna @Gram:Prep:Para.
#LATEX E: \emph{para} (cualquier otra ocurrencia)
#LATEX D: Requiere \emph{para}

#LATEX\section{Usos de \emph{por}}
#LATEXDOCU Veáse \url{http://dev.coerll.utexas.edu/spanishtx/spe/html/adv11.html}.

#LATEX\subsection{Usos de \emph{por} en locuciones}
##ADD (@Gram:Prep:Por:Locuciones) Prep IF (0 Por) (NOT 0 GramPrep) (1 LemasDePalabrasEnLocucionesConPor OR FormasDePalabrasEnLocucionesConPor);
ADD (@Gram:Prep:Por) Prep IF (0 Por) (NOT 0 GramPrep) (1 LemasDePalabrasEnLocucionesConPor OR FormasDePalabrasEnLocucionesConPor);
#LATEX T: Asigna @Gram:Prep:Por
#LATEX E: \emph{por ejemplo}, \emph{por eso}, \emph{por tanto}
#LATEX D: Requiere \emph{por} seguida por alguna palabra en la lista de palabras predefinidas \emph{LemasDePalabrasEnLocucionesConPor}

##ADD (@Gram:Prep:Por:Locuciones) Prep IF (0 Por) (NOT 0 GramPrep) (1 LoLiteral) (2 LemasDePalabrasEnLocucionesConPor);
ADD (@Gram:Prep:Por) Prep IF (0 Por) (NOT 0 GramPrep) (1 LoLiteral) (2 LemasDePalabrasEnLocucionesConPor);
#LATEX T: Asigna @Gram:Prep:Por.
#LATEX E: \emph{por lo tanto}, \emph{por lo regular}
#LATEX D: Requiere \emph{por} seguida por LO y alguna palabra en la lista de palabras predefinidas \emph{LemasDePalabrasEnLocucionesConPor}

#LATEX\subsection{Usos de \emph{por} no clasificados}
ADD (@Gram:Prep:Por) Prep IF (0 Por) (NOT 0 GramPrep);
#LATEX T: Asigna @Gram:Prep:Por.
#LATEX E: \emph{por} (cualquier ocurrencia)
#LATEX D: Requiere \emph{por}

#LATEX\subsection{Usos de preposiciones no clasificados}
ADD (@Gram:Prep) Prep (NOT 0 GramPrep);
#LATEX T: Asigna @Gram:Prep.
#LATEX E: \emph{PREP} (cualquier ocurrencia)
#LATEX D: Requiere \emph{PREP}

#LATEX\chapter{Uso de los pronombres}
## SECTION -- RULES TO ADD Prep-RELATED INFORMTATION

#LATEX\section{Pronombres de sujeto}

ADD (@Gram:Pron:Sujeto) PronPersFuerte;
#LATEX T: Asigna @Gram:Pron:Sujeto.
#LATEX E: \emph{yo, tú, él, ella, ud., nosotros, vosotros, ellos, ellas, uds.} (cualquier ocurrencia)
#LATEX D: La ocurrencia de uno de ellos

#LATEX\section{Pronombres de objeto directo o complemento atributivo}
ADD (@Gram:Pron:Objeto) ElLema IF (NOT -1 Prep) (0 Articulo) (1 FiniteVerbForms) ;
#LATEX T: Asigna @Gram:Pron:Objeto
#LATEX E: mí nunca me \emph{la} hicieron pero que estabamos
#LATEX D: \emph{lo, la, los, las} VERBO CONJUGADO

#LATEX\section{Pronombres de objeto directo, indirecto, o posesivos}
ADD (@Gram:Pron:Objeto) PronPers IF (0 PronombreDeObjetoNoAmbiguos);
#LATEX T: Asigna @Gram:Pron:Objeto
#LATEX E: \emph{me, te, os, nos} (cualquier ocurrencia)
#LATEX D: La ocurrencia de uno de ellos.

#LATEX\section{Pronombres de objeto indirecto}
ADD (@Gram:Pron:Objeto) PronPersClitico IF (0 LeLema);
#LATEX T: Asigna @Gram:Pron:Objeto
#LATEX E: \emph{le, les}
#LATEX D: La ocurrencia de uno de ellos.

#LATEX\section{Pronombres de objeto doble}
ADDRELATION (Gram:Pron:ObjetoDoble) PronPers (0 PronombreDeObjetoNoAmbiguos) TO (1 ElLema + Articulo);
ADDRELATION (Gram:Pron:ObjetoDoble) PronPersSE (0 SeLiteral) TO (1 ElLema + Articulo);
#LATEX T: Asigna @Gram:Pron:ObjetoDoble.
#LATEX E: \emph{se lo, se la, se los, se las, me lo, me la, me los, me las, te lo, te la, te los, te las,} etc.
#LATEX D: La ocurrencia de cualquier combinación de dos pronombres clíticos.

#LATEX\section{Pronombre se (solo)}
ADD (@Gram:Pron:Otros) PronPersSE (0 SeLiteral) (NOT 0 GramPron) (NOT 1 FiniteVerbForms);
#LATEX T: Asigna @Gram:Pron:Otros.
#LATEX E: \emph{se lo, se la, se los, se las, me lo, me la, me los, me las, te lo, te la, te los, te las,} etc.
#LATEX D: La ocurrencia de cualquier combinación de dos pronombres clíticos.

## SECTION -- RULES TO ADD PAST TENSE RELATED INFORMTATION
#LATEX\chapter{Formas verbales conjugadas}

#LATEX\section{Formas verbales para tiempos de indicativo}

#LATEX\subsection{Pretérito imperfecto}
# PRETÉRTIO IMPERFECTO
ADD (@Gram:Imperf) FiniteVerbForms IF (0 FiniteVerbForms + PretImperfSimple) (NOT 0 Haber) ;
#LATEX T: Asigna @Gram:Imperf.
#LATEX E: \emph{cantaba, corría, temía, partía} (cualquier ocurrencia)
#LATEX D: Requiere una forma identificada como pretérito simple que no sea del verbo haber (auxiliar).

#LATEX\subsection{Pretérito perfecto}

#LATEX\subsubsection{Pretérito perfecto simple}
# PRETÉRTIO PERFECTO SIMPLE
ADD (@Gram:Indefinido) FiniteVerbForms IF (0 FiniteVerbForms + PretPerfSimple) (NOT 0 Haber) ;
#LATEX T: Asigna @Gram:PretPerf.
#LATEX E: \emph{cantó, corrió, anduvo, partió} (cualquier ocurrencia)
#LATEX D: Requiere una forma identificada como pretérito perfecto simple que no sea del verbo haber (auxiliar).

#LATEX\subsubsection{Pretérito perfecto perifrástico o compuesto}\label{sec:HaberAuxPretPerfecto}
# PRETÉRTIO PERFECTO PERIFRÁSTICO
ADDRELATION (Gram:PerfPerif Gram:Aux:Haber) FiniteVerbForms (0 Haber + FiniteVerbForms + Presente) (NOT 0 Subjuntivo) TO (1 Part);
#LATEX T: Asigna R:Gram:PerfPerif a la forma del verbo HABER
#LATEX E: \emph{ha cantado, ha corrido, ha andado dormido} (cualquier ocurrencia)
#LATEX D: Requiere una forma finita de HABER seguida de un participio

#LATEX\section{Verbos piscológicos: gustar, encantar, sorprender y otros}
#LATEXDOCU Veáse \url{http://dev.coerll.utexas.edu/spanishtx/spe/html/adv13.html}.
#LATEXDOCU Veáse \url{http://dev.coerll.utexas.edu/spanishtx/spe/html/beg05.html}.

#LATEX\subsection{Usos de \emph{gustar} para expresar gustos propios}
ADDRELATION (Gram:Gustar:Propios) PronPers (0 Me OR NosLiteral) TO (1 Gustar-Like + 3aPersAmbig);
#LATEX T: Asigna @Gram:Gustar:Propios.
#LATEX E: \emph{me gusta} ayudar a mis amigos 
#LATEX D: Requiere la forma ME seguida de una forma de GUSTAR en tercera persona (posiblemente ambigua)

ADDRELATION (Gram:Gustar:Propios) PronPers (0 Me OR NosLiteral) (1 Haber) TO (1 Gustar-Like + Part);
#LATEX T: Asigna R:Gram:Gustar: Propios a la forma del verbo HABER
#LATEX E: \emph{me ha gustado} ayudar a mis amigos 
#LATEX E: \emph{me hubieras gustado} con un vestido verde 
#LATEX D: Requiere la forma ME seguida de una forma de HABER en tercera persona (posiblemente ambigua)

#LATEX\subsection{Usos de \emph{gustar} para expresar gustos de tu interlocutor}
ADD (@Gram:Gustar:Interlocutor) FiniteVerbForms IF  (-1 Tu) (0 Gustar-Like + 3aPersAmbig);
#LATEX T: @Gram:Gustar:Interlocutor
#LATEX E: Dime, \emph{te gusta} vivir en Estados Unidos.] 
#LATEX D: Secuencia del lema TE y un forma de GUSTAR en tercera persona del singular.

#LATEX\subsection{Usos de \emph{gustar} para expresar gustos de otras personas}
ADDRELATION (Gram:Gustar:Ajenos) FiniteVerbForms (0 Gustar-Like + 3aPersAmbig) TO (-1 LeLema);
#LATEX T: R:Gram:Gustar:Ajenos a la forma del verbo GUSTAR
#LATEX E: Dime, \emph{le gusta} vivir en Estados Unidosa tu hijo.] 
#LATEX D: Secuencia del lema LE y un forma de GUSTAR en tercera persona del singular.

#LATEX\section{Expresar sorpresa}
#LATEXDOCU Veáse \url{http://dev.coerll.utexas.edu/spanishtx/spe/html/adv13.html}.
#LATEX\subsection{Usos de \emph{sorprender}}

ADDRELATION (Gram:Gustar:Otros) FiniteVerbForms (0 Sorprender-Like + 3aPersAmbig) TO (-1 Yo);
#LATEX T: Asigna Gram:Gustar:Otros (Gram:ExpresarSorpresas:Propias) a la forma de SORPRENDER
#LATEX E: Siempre \emph{me sorprende} su forma de hablar.
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Gram:Gustar:Otros) FiniteVerbForms (0 Sorprender-Like + 3aPersAmbig) TO (-1 Tu);
#LATEX T: Asigna Gram:Gustar:Otros (antes era Gram:ExpresarSorpresas:Interlocutor) a la forma de SORPRENDER
#LATEX E: ¿Y \emph{te sorprende} su forma de hablar?
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Gram:Gustar:Otros) Part (0 Sorprender-Like) TO (*-1 Tu BARRIER GrupoNominal OR LimiteOracionSimple LINK 1 Haber + 3aPersAmbig);
#LATEX T: Asigna Gram:Gustar:Otros (antes era Gram:ExpresarSorpresas:Interlocutor) a la forma de HABER
#LATEX E: ¿Hay alguna palabra mexicana que \emph{te haya sorprendido}?
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Gram:Gustar:Otros) FiniteVerbForms (0 Sorprender-Like + 3aPersAmbig) TO (-1 LeLema);
#LATEX T: Asigna Gram:Gustar:Otros (antes era Gram:ExpresarSorpresas:Ajenas) a la forma de SORPRENDER
#LATEX E: ¿Y \emph{le sorprende} su forma de hablar?
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Gram:Gustar:Otros) Part (0 Sorprender-Like) TO (*-1 LeLema BARRIER GrupoNominal OR LimiteOracionSimple LINK 1 Haber + 3aPersAmbig);
#LATEX T: Asigna Gram:Gustar:Otros (antes era Gram:ExpresarSorpresas:Ajenas) a la forma de HABER
#LATEX E: ¿Hay alguna palabra mexicana que \emph{le haya sorprendido}?
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

#LATEX\section{Formas verbales para tiempos de subjuntivo}
ADD (@Gram:MSub:Pres) Verbo IF (0 Presente + Subjuntivo) (NOT -1 De) (NOT 1 Part);
ADD (@Gram:MSub:Pret) FiniteVerbForms IF (0 Verbo + Pasado + Subjuntivo) (NOT -1 De) (NOT 1 Part);
ADDRELATION (Gram:MSub:Pres) FiniteVerbForms (0 Haber + Verbo + Presente + Subjuntivo) (NOT -1 De) TO (1 Part);
ADDRELATION (Gram:MSub:Pret) FiniteVerbForms (0 Haber + Verbo + Pasado + Subjuntivo) (NOT -1 De) TO (1 Part);
#LATEX T: Asigna Gram:MSub:Pres a verbos en presente o pretérito de subjuntivo
#LATEX E: ¿Hay alguna palabra mexicana que \emph{le haya sorprendido}?
#LATEX D: No se aplica si la forma verbal no esta precedida por una "de" para evitar formas incorrectamente marcadas como subjuntivo por el tagging básico. Si se trata de una forma compuesta, es decir, HABER + PART, se marca todo como tal.

## SECTION -- RULES TO ADD PAST GUSTAR AND SIMILAR VERBS
#LATEX\section{Verbos copulativos}

#LATEX\subsection{Verbo ser}

ADD (@Gram:SerEstar:Ser) Verbo (0 VerboSer OR VerboSerAmbiguoConErar) (NOT -1 Haber);
ADD (@Gram:SerEstar:Ser) Verbo (0 VerboSerAmbiguoConIr) (NOT 1 Prep) (NOT -1 Haber);
ADDRELATION (Gram:SerEstar:Ser) Verbo (0 Haber + FiniteVerbForms) TO (1 VerboSer + Part OR VerboSerAmbiguoConErar + Part) (NOT 1 Part);
#LATEX T: @Gram:SerEstar:Ser
#LATEX E: \emph{soy, eres,} \emph{ha sido,}, pero no \emph{ha sido comprado}etc. 
#LATEX E: \emph{fui, fuimos...} NOT Prep 
#LATEX D: Cualquier forma de \emph{ser} del pretérito indefinido no seguido de una preposición (porque entonces seguramente será del verbo ir). También las formas perifrásticas con haber que no vayan seguidas de un participició

#LATEX\subsection{Verbo estar}
ADD (@Gram:SerEstar:Estar) Verbo (0 VerboEstar);
#LATEX T: @Gram:SerEstar:Estar
#LATEX E: \emph{estoy, estás, estaban,} etc. 
#LATEX D: Cualquier forma de \emph{estar}

#LATEX\section{Verbo haber}
#LATEX\subsection{Verbo haber en su uso com existencial}
ADD (@Gram:Haber:Exist) Verbo (0 Haber) (NOT 1 Part);
#LATEX T: @Gram:Haber:Exist
#LATEX E: \emph{Había} tres personas en el ascensor. 
#LATEX D: Cualquier forma de \emph{haber} que no vaya seguida de un participio

#LATEX\section{Verbos auxiliares: ser, estar y haber}

#LATEX\subsection{Verbo estar en su uso como auxiliar}
#LATEXDOCU Se etiqueta con RGram:SerEstar:EstarAux las secuencias de estar seguido de un gerundio.
ADDRELATION (Gram:SerEstar:EstarAux) VerboEstar IF (0 FiniteVerbForms) TO (1 Gerundio);
ADDRELATION (Gram:SerEstar:EstarAux) VerboEstar IF (0 Infinitive) TO (1 Gerundio);
#LATEX T: R:Gram:SerEstar:EstarAux
#LATEX E: y me \emph{estuvo platicando} allí
#LATEX D: Actualmente sólo se aplica sobre formas conjugadas y en secuencias estar (infinitivo) más gerundio. Habrá que ver cuán necesario es aplicar reglas para "hemos estado mirando" o "estando comprando?" (el interrogante los uso para marcar que dudo de la gramaticalidad de esta oración.

#LATEX\subsection{Verbo haber en su uso como auxiliar en tiempos verbales compuestos}
#LATEXDOCU Se etiqueta con @Gram:Aux:Haber, pero se hace en las reglas que sirven para etiquetar los tiempos verbales correspondientes. Véase por ejemplo la anotación (y la regla) del Presente Perfecto en la sección \ref{sec:HaberAuxPretPerfecto}. 

#LATEX\subsection{Verbo ser en su uso como auxiliar}
###LATEXDOCU No se etiquetan las ocurrencias de ser como auxiliar en las construcciones pasivas por dos razones: una es que hay muy pocas ocurrencias (unas 10 o 12) y otra es que se confunden a menudo con usos adjetivales del participio (por ejemplo, \emph{soy callada y reservada}, \emph{la otra era licenciada en administración de empresas}, etc. Entre unos y otros había unos 31 casos en el Batch 1 (los primeros 327 clips publicados). Véase la Figura \ref{fig:InstancesOfSerPlusParticiple}.

#LATEXDOCU \begin{figure}[htb]
#LATEXDOCU \centering
#LATEXDOCU \includegraphics[scale=0.5]{Figs/InstancesOfSerPlusParticiple.png}
#LATEXDOCU \caption{Ocurrencias de ser seguido de participio en Batch 1.}
#LATEXDOCU \label{fig:InstancesOfSerPlusParticiple}
#LATEXDOCU \end{figure}

## SECTION -- RULES TO ADD PAST GUSTAR AND SIMILAR VERBS
#LATEX\chapter{Funciones comunicativas}

## SECTION -- RULES TO ADD PAST GUSTAR AND SIMILAR VERBS
#LATEX\chapter{Pragmática}

#LATEX\section{Marcadores discursivos}
#LATEXDOCU Véase \url{http://www.cs.famaf.unc.edu.ar/~laura/shallowdisc4summ/index.html}.

#LATEX\subsection{Marcadores discursivos indicadores de revisión}
## Prag:MarcadoresDisc:Revision

ADD (@Prag:MarcadoresDisc:Revision) MarcDiscRevision;
#LATEX T: @:Prag:MarcadoresDisc:Revision
#LATEX E: \emph{aunque}, \emph{excepto} o \emph{pero}
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]
# aunque
# excepto
# pero

ADDRELATION (Prag:MarcadoresDisc:Revision) Sin TO (1 Embargo);
#LATEX T: R:Prag:MarcadoresDisc:Revision (relación entre el primer i el último elemento de la locución)
#LATEX E: \emph{sin embargo}
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Prag:MarcadoresDisc:Revision) De TO (1 Hecho);
#LATEX T: R:Prag:MarcadoresDisc:Revision (relación entre el primer i el último elemento de la locución)
#LATEX E: \emph{de hecho}
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Prag:MarcadoresDisc:Revision) En TO (1 Realidad);
#LATEX T: R:Prag:MarcadoresDisc:Revision (relación entre el primer i el último elemento de la locución)
#LATEX E: \emph{en realidad}
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

# 2 tokens (NO TRATADOS)
# pese a
# salvo que
#ADDRELATION (Prag:MarcadoresDisc:Revision) No (1 Obstante);
# no obstante
# al contrario
# con todo
# ahora bien

# 3 tokens
ADDRELATION (Prag:MarcadoresDisc:Revision) APrep (1 Pesar) TO (2 De);
#LATEX T: R:Prag:MarcadoresDisc:Revision (relación entre el primer i el último elemento de la locución)
#LATEX E: \emph{a pesar de}
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Prag:MarcadoresDisc:Revision) De (1 Todos) TO (2 Modos);
#LATEX T: R:Prag:MarcadoresDisc:Revision (relación entre el primer i el último elemento de la locución)
#LATEX E: \emph{de todos modos}
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Prag:MarcadoresDisc:Revision) Es (1 Cierto) TO (2 Que);
#LATEX T: R:Prag:MarcadoresDisc:Revision (relación entre el primer i el último elemento de la locución)
#LATEX E: \emph{s cierto que}
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

# 4 tokens (NO TRATADOS)
# el hecho es que

#LATEX\subsection{Marcadores discursivos indicadores de causa}

ADD (@Prag:MarcadoresDisc:Causa) Porque;
#LATEX T: @Prag:MarcadoresDisc:Causa
#\LATEX E: (...) y es curioso \emph{porque} hace más o menos una semana que lo vi (...)
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Prag:MarcadoresDisc:Causa) Prep IF (0 Por) (1 LoLiteral) TO (2 Tanto);
ADDRELATION (Prag:MarcadoresDisc:Causa) Prep IF (0 Por) TO (2 Tanto OR FormasDePalabrasEnLocucionesCausalesConPor);
#LATEX T: Asigna @Prag:MarcadoresDisc:Causa.
#LATEX E: \emph{por lo tanto}
#LATEX E: \emph{por eso}
#LATEX D: Requiere \emph{por} seguida por LO y TANTO

ADDRELATION (Prag:MarcadoresDisc:Causa) Debido TO (1 APrep);
#LATEX T: R:Prag:MarcadoresDisc:Causa (relación entre el primer i el último elemento de la locución)
#\LATEX E: (...) mi propio negocio pues \emph{debido a} unas dificultades (...)
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Prag:MarcadoresDisc:Causa) Gracia (0 PL) (NOT 2 Dios) TO (1 APrep);
#LATEX T: R:Prag:MarcadoresDisc:Causa (relación entre el primer i el último elemento de la locución)
#\LATEX E: \emph{gracias a} la tecnología este sí conservo amistades (...)
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Prag:MarcadoresDisc:Causa) Gracia (0 PL) (1 APrep) TO (2 Dios);
#LATEX T: R:Prag:MarcadoresDisc:Causa (relación entre el primer i el último elemento de la locución)
#LATEX E: y de ahí fui a la ceremonia y \emph{gracias a dios} me hice ciudadano
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

#LATEX\subsection{Marcadores discursivos indicadores de igualdad}
## Prag:MarcadoresDisc:Igualdad

ADD (@Prag:MarcadoresDisc:Igualdad) Adv IF (0 Ademas) (NOT 1 De);
ADD (@Prag:MarcadoresDisc:Igualdad) Adv IF (0 Tambien);
#LATEXREM No añado \emph{precisamente} porque es ambiguo entre su lectura literal (preciso, justo, cierto) como modificador verbal y su lectura como marcador discursivo. Para anotarlo con fiabilidad habría que tener en cuenta su posición respecto al principio de la oración y el verbo principal. Ejemplos: \emph{sí muchas pero no sé precisamente cuál cuál sería una}, \emph{allí vivimos como un año y precisamente yo me tomé me tomaron fotos en un un estudio de fotografía}
#LATEX T: @:Prag:MarcadoresDisc:Igualdad
#LATEX E: \emph{además}, \emph{también}
#LATEX D: Requiere \emph{además} o \emph{también} no seguido de DE

ADDRELATION (Prag:MarcadoresDisc:Igualdad) Prep IF (0 Por) TO (1 Ejemplo);
#LATEX T: R:Prag:MarcadoresDisc:Igualdad (relación entre el primer i el último elemento de la locución)
#LATEX E: \emph{por ejemplo}
#LATEX D: Requiere \emph{por} seguida por alguna palabra en la lista de palabras predefinidas \emph{LemasDePalabrasEnLocucionesConPor}

ADDRELATION (Prag:MarcadoresDisc:Igualdad) ConjLetraO TO (1 Sea);
#LATEX T: R:Prag:MarcadoresDisc:Igualdad
#LATEX E: \emph{o sea}
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Prag:MarcadoresDisc:Igualdad) No TO (1 SoloAcento);
#LATEX T: R:Prag:MarcadoresDisc:Igualdad
#LATEX E: porque uno \emph{no sólo} puede ayudar en el aspecto de inglés pero en el aspecto del español también
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Prag:MarcadoresDisc:Igualdad) Tal TO (1 Vez);
#LATEX T: R:Prag:MarcadoresDisc:NoTipificado
#LATEX E: noto que \emph{tal vez} sus hijos hablan el spanglish más unos más que otros
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

#LATEX\subsection{Elementos discursivos usadors para ganar tiempo}
## Prag:MarcadoresDisc:Revision

ADD (@Prag:RecuDiscurso:GanarTiempo) ArtPronDemo IF (NOT -1 Prep) (0 EsteLiteral) (NOT 1 Nombre);
#LATEX T: @Prag:ElementosDiscurso:GanarTiempo
#LATEX E: \emph{Este} en mi niñez era un poco más soñadora mucho más soñadora de hecho (...)
#LATEX D: Anota ocurrencies de ESTE que no vayan precedidas de una preposición ni seguidas de un sustantivo.

## SECTION -- RULES TO ADD PAST GUSTAR AND SIMILAR VERBS
#LATEX\chapter{Estructuras características del habla en hablantes de herencia}

#LATEX\section{Estructuras derivadas del contacto entre lenguas}
## Herit:Contacto:Mixtas

#LATEX\subsection{Expresiones con cambio de código}
#LATEXDOCU Incluímos aquí expresiones que mezclan palabras en ambas lenguas, en este caso el español y el inglés. En la bibliografía especializada se utiliza el término inglés ``code switching''.

## TO DO: NEEDS DIFFERENT INPUT TO WORK
ADDRELATION (Herit:Contacto:Mixtas) Verbo IF (0 ListaVerbosCorpusParaContactoMixtos) TO (*1 PalabraEnIngles BARRIER PalabraEnEspanol);
#LATEX T: R:Herit:Contacto:Mixtas
#LATEX E: \emph{hacer request}, \emph{comer turkey}
#LATEX D: Anota secuencias de un verbo seguid de una o más palabras en inglés. Por ahora lo limitamos a ser, hacer, decir y comer porque TreeTagger anota como verbo algunas palabras que no lo son y no queremos generar ruido para los usuarios.

### FITA: no s'està mapejant la info de llengua i per tant la regla anterior no funciona
### 02/21/2013

#LATEX\chapter{Vocabulario multipalabra}
## Vocab

#LATEX\section{Nombres propios}

#LATEX\subsection{Nombres de localizaciones geográficas}
ADDRELATION (Vocab:Entities:Geo) Articulo IF  (0 ("<El>")) TO (1 ("<Paso>"));
ADDRELATION (Vocab:Entities:Geo) Nombre IF  (0 ("<Estados>")) TO (1 ("<Unidos>"));
ADDRELATION (Vocab:Entities:Geo) Nombre IF  (0 ("<Ciudad>")) TO (1 ("<Juárez>"));
#LATEX T: R:Vocab:Entities:Geo
#LATEX E: \emph{Ciudad Juárez}, \emph{El Paso}, \emph{Estados Unidos}  
#LATEX D: Reglas para anotar entidades frecuentes en el corpus. En este caso de nombres geográficos.

#LATEX\subsection{Combinaciones de verbo y preposición de régimen}
ADDRELATION (Vocab:Fraseol:VerbPrep) Verbo IF  (0 ("hablar")) TO (*1 Prep BARRIER LimiteOracionSimple OR GrupoNominal OR Verbo);
ADDRELATION (Vocab:Fraseol:VerbPrep) Verbo IF  (0 ("pensar")) TO (*1 Prep BARRIER LimiteOracionSimple OR GrupoNominal OR Verbo);
ADDRELATION (Vocab:Fraseol:VerbPrep) Verbo IF  (0 ("empezar")) TO (*1 Prep BARRIER LimiteOracionSimple OR GrupoNominal OR Verbo);
#LATEX T: R:Vocab:Fraseol:VerbPrep
#LATEX E: \emph{hablar PREP}, \emph{pensar PREP}, \emph{empezar PREP}  
#LATEX D: Ahora no se distingue entre usos lexicalizados y usos composicionales (p.e., ``empezar en San Antonio y terminar en Austin'' se trata igual que ``empezé con la tradición en la preparatoria'') 
##AFTER-SECTIONS


#ADD (@Gram:Prep:Por:Locuciones) Prep IF (0 Por) (1 FormasFrecEnLocPor);


#ADDRELATION (Gram:PerfPerif) VerboConjugado (0 Haber) TO (1 Participio);


#ADD (@Gram:Det:Art) Articulo IF (0 Lo) (*1 Adj BARRIER NPBoundary);

