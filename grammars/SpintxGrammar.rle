MAPPING-PREFIX = @ ;
DELIMITERS = "<$.>" "<$?>" "<$!>" "<$:>";
##PARENTHESES = ("<(>" "<)>") ("<[>" "<]>") ("<{>" "<}>") ("<...>)
## -- #LATEX T: [Espacio para la etiqueta que asigna la regla.]
## -- #LATEX E: [Espacio para incluir \emph{un ejemplo} con la parte relevante en cursiva.] 
## -- #LATEX D: [Espacio para describir con palabras lo que hace la regla.]

### Comment this to avoid problems with semicolon now
### DELIMITERS = "<$.>" "<$?>" "<$!>" "<$:>" "<$\;>" ;

#LATEX\chapter*{Introducción}
#LATEXDOCU Este documento describe el proceso de anotación de las transcripciones de los clips del corpus SPinTX (Spanish in Texas) con información pedagógica para facilitar el uso de los mismos en la enseñanza y el aprendizaje de español como lengua extranjera.

#LATEXDOCU

#LATEX\part{Definición de LIST y SET}
#LATEX\chapter{LIST y SET basados en información morfosintáctica}
#LATEX\section{Categorías gramaticales}

## LISTS OR SETS FOR PART-OF-SPEECH TAGS
LIST ArtPronDemo = (DM);
LIST Nombre = (Noun);
LIST Adverbio = (ADV);
LIST Prep = (Preposition) (PREP);
LIST PrepContract = (PDEL);
LIST FiniteVerbForms = (VLfin) (VEfin) (VHfin) (VSfin) (VMfin) ;
LIST FiniteFormsInPast = (VLfin Indi Pret) (VEfin Indi Pret) ;
LIST PretImperfSimple = (Imp Indi) ;
LIST PretPerfSimple = (Pret Indi) ;
LIST Adj = (ADJ) (ORD) ;
LIST AdjOrd = (ORD) ;
LIST Part = (VLadj) (VSadj);
LIST Verbo = (Verb);
LIST Infinitive = (VLinf) (VEinf) (VHinf) (VSinf);
LIST Gerundio = (VLger) (VEger) (VHger) (VSger);
LIST PronombrePersonal = (PPX);
LIST DetAllByPOS = (ART) (DM) (PPO) ("alguno" QU); # habrá que refinar la definición, p.e., incluye palabras como mío, mía, míos, que claramente son adjetivos y no determinantes (esto viene de TreeTagger)
LIST DetQU = (Determiner QU); #
LIST Sustantivo = (NC) (NP) (NMEA) (NMON) ;
LIST NombrePropio = (NP);
LIST Articulo = (ART);
LIST Conjuncion = (Conjunction);
SET AdjOrPart = Adj OR Part ;
SET FiniteVerbFormsOrPart = FiniteVerbForms OR Part ;
LIST PronPers = (PPX);
LIST PronPersClitico = (PPC);
LIST PronPersSE = (SE);
LIST PalabraEnIngles = (lang:en);
LIST PalabraEnEspanol = (lang:es);
LIST Pasado = (Past); #only for subjunctive (?)
LIST Presente = (Pres);
LIST Futuro = (Fut);
LIST Subjuntivo = (Subj);
LIST Imperativo = (Command);
LIST LimiteOracionSimple = (CC) (CCAD) (CSUBX) (CSUBF) (CQUE) (CSUBI) ; # habrá que refinar la definición
LIST Puntuacion = (Punctuation);
LIST DetCardinales = (CARD);
LIST PuntuacionComa = (Punctuation CM);

#LATEX\section{Rasgos morfosintácticos}
## LIST MORPH FEATURES VERBS

LIST PL = (Plural) (Plu);
LIST SG = (Sing) (Singular);
LIST 1aPers = (1st);
LIST 3aPers = (3rd);
LIST 2aPers = (2nd);
LIST 1aO3aPersAmbig = (1st_or_3rd);

SET 3aPersAmbig = 3aPers OR 1aO3aPersAmbig;

#LATEX\chapter{LIST y SET basados en lemas}
#LATEX\section{Los LIST}
## LISTS OR SETS FOR LEMMATA
LIST APrep = ("a");
LIST Pesar = ("pesar");

LIST Por = ("por");
LIST Para = ("para");
LIST Haber = ("haber") ;
LIST Personas = ("niño") ("mujer") ("hombre") ("chico") ("viejo") ("mayor") ("hijo") ("estudiante") ("muchacho") ("persona");
LIST Ser = ("ser");
LIST Servir = ("servir");
LIST Bueno = ("bueno");
##LATEXREM Tal como está se aplicará a ``bueno'' y a ``mejor'' por igual. No se distinguen. 
LIST Malo = ("malo");
LIST No = ("no");
LIST Gustar-Like = ("gustar") ("interesar") ("fascinar") ("encantar");
LIST Sorprender-Like = ("sorprender");
LIST Yo = ("yo" PPX);
LIST Tu = ("tú" PPX);
LIST Nos = ("nosotros" PPX);
LIST Os = ("vosotros" PPX);
LIST LeLema = ("él" PPC);
LIST PretPerfRelations = (r:PretPerifAux) (r:PretPerifMain);
LIST Que = ("que");
LIST QueConAcento = ("qué" INT);
LIST UstedLema = ("usted");
LIST ConjLetraO = ("o");
LIST MarcDiscRevision = ("pero") ("aunque") ("exepto");
LIST Sin = ("sin");
LIST Embargo = ("embargo");
LIST De = ("de");
LIST DeODel = ("de") ("del");
LIST Hecho = ("hecho") ("<hecho>");
LIST En = ("en");
LIST Realidad = ("realidad");
LIST Cierto = ("cierto");
LIST Debido =("deber|debido");
LIST Porque =("porque");
LIST Gracia =("gracia"); #now used to handle "gracias" which does not have a lemmatisation of its own, which is probably ok
LIST Dios =("dios");
LIST SoloAcento =("sólo");
LIST ElLema = ("el");
LIST VerboSer = ("ser");
LIST VerboHablar = ("hablar");
LIST VerboSerAmbiguoConErar = ("erar|ser"); ## eras, eran, era, eres (no éramos).
LIST VerboSerAmbiguoConIr = ("ser|ir"); ## fui, fuimos, fue, fuiste...
LIST VerboEstar = ("estar");
LIST ListaVerbosCorpusParaContactoMixtos = ("ser") ("decir") ("hacer") ("hacerse");
LIST ConLema = ("con");
LIST FormasDePalabrasEnLocucionesConPor = ("<ello>") ("<eso>");
LIST LemasDePalabrasEnLocucionesConPor = ("ejemplo") ("tanto") ("ello") ("eso");
LIST LemasDePalabrasEnLocucionesConPorLo = ("regular") ("tanto") ("pronto");
LIST Tanto = ("tanto");
LIST FormasDePalabrasEnLocucionesCausalesConPor = ("<ello>") ("<eso>");
LIST Ejemplo = ("ejemplo");
LIST Ademas = ("además");
LIST Tambien = ("también");
LIST Vez = ("vez");
LIST Tal = ("tal");
LIST EsteEseAquel = ("ese") ("este") ("aquel");
LIST SustMascAcabadosEnA = ("idioma") ("día") ("problema") ("programa") ("tema") ("sistema") ("lema") ("planeta") ("síntoma") ("trauma");
LIST SustFemConATonicaInicial = ("área") ("agua") ("habla") ("arma") ("alma");
LIST NoAdv = ("no" NEG);
LIST VerbosExpresarDeseos = ("esperar") ("pedir") ("gustar") ("encantar");
LIST NombresExpresarDeseos = ("esperanza") ("necesidad") ("petición") ("demanda") ;
LIST VerbosExpresarRecomendaciones = ("recomendar") ("sugerir") ("aconsejar") ("necesitar");
LIST LemaIr = ("ir");
LIST AdjetivosUsadosEnRecomendaciones = ("importante") ("bueno");
LIST LemasQueNoSonDetQUStandard = ("todo") ("nada") ("cualquier") ("tal") ("cada") ;
LIST QuizasConOSinEse = ("quizás") ("quizá");
LIST ListaDeDeterminantesSeguros = ("cualquier") ("cada") ("ambos") ("alguno") ("tal") ("ninguno");
LIST ListaDeDeterminantesSegurosSiNoEstanEnMasc = ("mucho") ("tanto") ("demasiado") ("cierto") ("varios") ("diversos") ("poco");
## ("dentista")
LIST NingunoLiteral = ("<Ninguno>") ("<ninguno>") ("<NINGUNO>");
##de momento no se usa
LIST VerboSaberAmbiguoConSer = ("saber|ser");
LIST OtroLema = ("otro");
LIST PocoLema = ("poco");
LIST AlgoLema = ("algo");
LIST NadaLema = ("nada");
LIST AdvIntensificadorAdjetivos = ("muy") ("más") ("tan");
LIST SustantivosQueDenotanNecesidades = ("descanso") ("ayuda") ("baño") ("automóvil") ("leche") ("comida") ("hora") ("tiempo") ("día") ("año") ("dinero") ("plata");
LIST NecesitarLema = ("necesitar");
LIST NecesarioLema = ("necesario");
LIST MuchoLema = ("mucho");
LIST DarLema = ("dar");
LIST AgradecerLema = ("agradecer");

#LATEX\section{Los SET}
SET BuenoOMalo = Bueno OR Malo;
SET PorOPara = Por OR Para;
SET SerOServir = Ser OR Servir;

#LATEX\chapter{LIST y SET basados en palabras (literales)}
LIST MismoLiteral = ("<mismo>") ("<Mismo>") ("<MISMO>") ;
LIST OjalaLiteral = ("<ojalá>") ("<Ojalá>") ("<OJALÁ>");
LIST CreenLiteral = ("<creen>") ("<CREEN>") ("<Creen>");
LIST YoLiteral = ("<yo>") ("<Yo>") ("<YO>") ;
LIST TuLiteral = ("<tú>") ("<Tú>") ("<TÚ>") ;
LIST ElOEllaLiterales = ("<él>") ("<Él>") ("<ÉL>") ("<ella>") ("<Ella>") ("<ELLA>") ;
LIST NosotrosLiteral = ("<nosotros>") ("<Nosotros>") ("<NOSOTROS>") ;
LIST VosotrosLiteral = ("<vosotros>") ("<Vosotros>") ("<VOSOTROS>") ;
LIST EllosOEllasLiteral = ("<ellos>") ("<Ellos>") ("<ELLOS>") ("<ellas>") ("<Ellas>") ("<ELLAS>") ;
LIST Sea = ("<sea>") ("<SEA>") ("<Sea>");
LIST LoLiteral = ("<lo>") ("<LO>") ("<Lo>");
LIST Me = ("<me>") ("<ME>") ("<Me>");
LIST SeLiteral = ("<se>") ("<SE>") ("<Se>");
LIST NosLiteral = ("<nos>") ("<NOS>") ("<Nos>");
LIST Te = ("<te>") ("<TE>") ("<Te>");
LIST OsLiteral = ("<os>") ("<OS>") ("<Os>");
LIST EsteLiteral = ("<este>") ("<ESTE>") ("<Este>");
LIST EstoLiteral = ("<esto>") ("<ESTO>") ("<Esto>");
LIST EsoLiteral = ("<eso>") ("<ESO>") ("<Eso>");
LIST EseLiteral = ("<ese>") ("<ESE>") ("<Ese>");
LIST AquelloLiteral = ("<aquello>") ("<AQUELLO>") ("<Aquello>");
LIST AquelLiteral = ("<aquel>") ("<AQUEL>") ("<Aquel>");
LIST EranLiteral = ("<eran>") ("<Eran>") ("<ERAN>");
LIST ErasLiteral = ("<eras>") ("<Eras>") ("<ERAS>");
LIST EraMayusLiteral = ("<Era>") ("<ERA>");
LIST DiLiteral = ("<di>") ("<Di>") ("<DI>") ;
LIST LosLiteral = ("<los>") ("<Los>") ("<LOS>");
LIST ElDetLiteral = ("<el>") ("<El>") ("<EL>");
LIST UnDetLiteral = ("<un>") ("<Un>") ("<UN>");
LIST LaLiteral = ("<la>") ("<LA>") ("<La>");
LIST LasLiteral = ("<las>") ("<LAS>") ("<Las>");
LIST LiteralPrimero = ("<primero>") ("<PRIMERO>") ("<Primero>");
LIST LiteralTercero = ("<tercero>") ("<TERCERO>") ("<Tercero>");
LIST UnoLiteral = ("<uno>") ("<UNO>") ("<Uno>");
LIST MejorLiteral = ("<mejor>") ("<MEJOR>") ("<Mejor>");
LIST PorLiteral = ("<por>") ("<POR>") ("<Por>");
LIST MedioLiteral = ("<medio>") ("<MEDIO>") ("<Medio>");
LIST DemasLiteral = ("<demás>") ("<DEMÁS>") ("<Demás>");
LIST MasLiteral = ("<más>") ("<MÁS>") ("<Más>");
LIST YLiteral = ("y") ("Y");
SET LiteralPrimeroOrTercero = LiteralTercero OR LiteralPrimero;

LIST MiSingLiteral = ("<mi>") ("<MI>") ("<Mi>");
LIST MiPluLiteral = ("<mis>") ("<MIS>") ("<Mis>");
LIST TuSingLiteral = ("<tu>") ("<TU>") ("<Tu>");
LIST TuPluLiteral = ("<tus>") ("<TUS>") ("<Tus>");
LIST SuSingLiteral = ("<su>") ("<SU>") ("<Su>");
LIST SuPluLiteral = ("<sus>") ("<SUS>") ("<Sus>");
LIST NuestroSingLiteral = ("<nuestro>") ("<NUESTRO>") ("<Nuestro>");
LIST NuestroPluLiteral = ("<nuestros>") ("<NUESTROS>") ("<Nuestros>");
LIST VuestroSingLiteral = ("<vuestro>") ("<VUESTRO>") ("<Vuestro>");
LIST VuestroPluLiteral = ("<vuestros>") ("<VUESTROS>") ("<Vuestros>");
LIST NuestraSingLiteral = ("<nuestra>") ("<NUESTRA>") ("<Nuestra>");
LIST NuestraPluLiteral = ("<nuestras>") ("<NUESTRAS>") ("<Nuestras>");
LIST VuestraSingLiteral = ("<vuestra>") ("<VUESTRA>") ("<Vuestra>");
LIST VuestraPluLiteral = ("<vuestras>") ("<VUESTRAS>") ("<Vuestras>");


SET DetPosSing = MiSingLiteral OR TuSingLiteral OR SuSingLiteral;
SET DetPosPlu = MiPluLiteral OR TuPluLiteral OR SuPluLiteral;
SET DetPosFemPlu = NuestraPluLiteral OR VuestraPluLiteral;
SET DetPosMascPlu =  NuestroPluLiteral OR VuestroPluLiteral;
SET DetPosFemSing = NuestraPluLiteral OR VuestraPluLiteral;
SET DetPosMascSing =  NuestroPluLiteral OR VuestroPluLiteral;

SET DetAllIncluded = DetAllByPOS OR DetPosSing OR DetPosPlu OR DetPosFemPlu OR DetPosMascPlu OR DetPosFemSing OR DetPosMascSing;

SET DetAllIncludedNoPosPlural = DetAllByPOS OR DetPosSing OR DetPosFemSing OR DetPosMascSing;

SET GrupoNominal = Sustantivo OR Adj OR DetAllIncluded; # habrá que refinar la definición de grupo nominal

LIST ElLiteralMayusc = ("<El>");
LIST PasoLiteralMayusc = ("<Paso>");
LIST EstadosLiteralMayusc = ("<Estados>");
LIST UnidosLiteralMayusc = ("<Unidos>");

LIST Todas = ("<todas>") ("<TODAS>") ("<Todas>");
LIST Todos = ("<todos>") ("<TODOS>") ("<Todos>");
LIST Toda = ("<toda>") ("<TODA>") ("<Toda>");
LIST Todo = ("<todo>") ("<TODO>") ("<Todo>");
SET TodosYTodasLiteral = Todos OR Todas;
SET TodoYTodaLiteral = Todo OR Toda;
LIST Modos = ("<modos>") ("<MODOS>") ("<Modos>");
LIST Es = ("<es>") ("<Es>") ("<ES>");

SET DetArtDefinidosLiterales = ElDetLiteral OR LosLiteral OR LasLiteral OR LaLiteral;
SET PronPersFuerte = YoLiteral OR TuLiteral OR ElOEllaLiterales OR NosotrosLiteral OR VosotrosLiteral OR EllosOEllasLiteral OR UstedLema;
SET PronombreDeObjetoNoAmbiguos = Me OR Te OR NosLiteral OR OsLiteral;
SET PronombresCliticosTODOS = Me OR Te OR NosLiteral OR OsLiteral OR LosLiteral OR LasLiteral OR LaLiteral OR LoLiteral OR LeLema;
SET EstoEsoAquello = EstoLiteral OR EsoLiteral OR AquelloLiteral;

#LATEX\chapter{LIST y SET basados en etiquetas pedagógicas}
## PEDAGOGICAL TAGS
LIST GramPrep = (@Gram:Prep:Por) (@Gram:Prep:Por:Locuciones) (@Gram:Prep:Para) (@Gram:Prep:Para:Opinion) (@Gram:Prep:Para:Destinatario) (@Gram:Prep:Para:Habilidad);
LIST GramPron = (@Gram:Pron:Otros) (@Gram:Pron:ObjetoDoble);
LIST YaAnotadoComoNecesidad = (R:Func:Necesidades);
##("<(.*)(ar|er|ir|ír)>"r)

#LATEX\chapter{Definiciones de \emph{templates}}

#LATEXDOCU Los ``templates'' son un tipo de entidad especial en CG3 que sirve para expresar contextos o secuencias de palabras, lemas o etiquetas morfosintácticas que ocurren frecuentemente. 

TEMPLATE VP-VerbosExpresarDeseos = (0 VerbosExpresarDeseos) OR (0 VerbosExpresarDeseos LINK 1 Adverbio OR MejorLiteral);
TEMPLATE IniciadoresDeSubordinadasConQue = (1 Que) OR (1 Adverbio LINK 1 Que) OR (1 MejorLiteral LINK 1 Que);
TEMPLATE SintagmaNominal-Y-O-Clitico = (-1 Sustantivo OR Adj LINK -1 DetAllIncluded) OR (-1 PronombresCliticosTODOS OR SeLiteral);
TEMPLATE SintagmaPreposicional = (-1 Sustantivo LINK -1 DetAllIncluded LINK -1 Prep) OR (-1 Sustantivo LINK -1 Prep);
TEMPLATE SintagmaNominalHaciaLaIzda = (1 DetAllIncluded LINK 1 Sustantivo OR Adj) OR (1 DetAllIncluded LINK 1 Adj LINK 1 Sustantivo);

#LATEX\part{Secciones previas}
BEFORE-SECTIONS

SUBSTITUTE ("por" Adverb ADV) ("por" Preposition PREP) TARGET ("por");
SUBSTITUTE ("por" PDEL) ("por" Preposition PREP) TARGET ("por");

#LATEX\chapter{Reglas para rectificar errores del análisis morfosintático}
SUBSTITUTE (Noun NC Singular Masculine) (Conjunction CCAD) TARGET ("pero");
#LATEX T: CC
#LATEX E: \emph{pero} 
#LATEX D: Regla para cambiar a \emph{pero} la lectura de nombre por la de conjunción. Ahora se aplica indiscriminadamente, pero podría ser conveniente controlar que antes del pero no hay un artículo. Por ejemplo: ``el pero de la cuestión es que podría romperse el motor''.

SUBSTITUTE ("hecho" Noun NC Singular Masculine) ("hacer" Verb VLadj) TARGET ("hecho") IF (-1 Haber + Verbo);
#LATEX T: Cambia Noun NC Singular Masculine por Verb VLadj
#LATEX E: \emph{hecho} 
#LATEX D: Regla para cambiar a \emph{pero} la lectura de nombre por la de conjunción. Ahora se aplica indiscriminadamente, pero podría ser conveniente controlar que antes del pero no hay un artículo. Por ejemplo: ``el pero de la cuestión es que podría romperse el motor''.

SUBSTITUTE ("ranchar" Verb VLfin Pres Indi Sing 1st) ("rancho" Noun NC Singular Masculine) TARGET ("ranchar");
#LATEX T: Cambia Verb VLfin Pres Indi Sing 1st por Noun NC Singular Masculine en la palabra ``rancho''.
#LATEX E: \emph{rancho} 
#LATEX D: Lo hace de forma indiscriminada. Quizá en el futuro habrá que cambiarla, refinarla.

SUBSTITUTE ("dicho" Determiner QU) ("decir" Verbo VLadj) TARGET ("dicho");
#LATEX T: Cambia lectura de ``dicho'' como determinante por lectura como adjetivo/participio.
#LATEX E: \emph{dicho} 
#LATEX D: Lo hace de forma indiscriminada. Quizá en el futuro habrá que cambiarla, refinarla.

SUBSTITUTE ("oír" Verb VLfin Pres Subj Sing 1st_or_3rd) ("oír" Verb VLfin Pres Indi_Command Sing 2nd_3rd) TARGET ("oír");
#LATEX T: Cambia lectura de "oye" como presente de subjuntivo por lectura de presente de indicativo/imperativo.
#LATEX E: \emph{oye} 
#LATEX D: Lo hace de forma indiscriminada. Quizá en el futuro habrá que cambiarla, refinarla.

SUBSTITUTE ("ser" Verb VSfin Pret Indi Plural 3rd) ("ir" Verb VLfin Pret Indi Plural 3rd) TARGET ("<fueron>") IF (-1C SeLiteral);
SUBSTITUTE ("ser" Verb VSfin Pret Indi Plural 3rd) ("ir" Verb VLfin Pret Indi Plural 3rd) TARGET ("<fueron>") IF (1 ConLema + Prep);
SUBSTITUTE ("ser" Verb VSfin Pret Indi Plural 3rd) ("ir" Verb VLfin Pret Indi Plural 3rd) TARGET ("<fueron>") IF (1 Gerundio);
#LATEX T: Reglas que cambian la lectura de "ser" por "ir" en "fueron" cuando no va precedida de un pronombre se o seguida la preposición con o un gerundio.
#LATEX E: \emph{se fueron juntos, se fueron a la escuela} 
#LATEX D: Quizá en el futuro habrá que cambiarlas, refinarlas.

SUBSTITUTE ("maestro" Adjective ADJ Singular) ("maestro" Noun Singular) TARGET ("maestro");
SUBSTITUTE ("maestro" Adjective ADJ Plural) ("maestro" Noun Plural) TARGET ("maestro");
#LATEX T: Cambia lectura de "maestro" de adjetivo sustantivo.
#LATEX E: \emph{maestro, maestra, maestros, maestras} 
#LATEX D: Lo hace de forma indiscriminada. Quizá en el futuro habrá que cambiarla, refinarla. Por ejemplo, en "obras maestras", que no ocurre en la versión actal del corpus.

SUBSTITUTE ("erar|ser" Verb VLfin Past Subj Sing 2nd) ("ser" Verb VSfin Imp Indi Sing 2nd) TARGET ErasLiteral;
SUBSTITUTE ("erar|ser" Verb VLfin Past Subj Plural 2nd_or_3rd) ("ser" Verb VSfin Imp Indi Plural 3rd) TARGET EranLiteral;
SUBSTITUTE ("UNK" Noun NC) ("ser" Verb VSfin Imp Indi Sing 1st_or_3rd) TARGET EraMayusLiteral;
#LATEX T: Cambia lectura de "eras" y "eran" de lema ambiguo entre erar y ser a sólo lema ser.
#LATEX E: \emph{eras, eran} 
#LATEX D: Consideramos erar un verbo muy poco frecuente. Si hiciera falta, se podría refinar la regla.

SUBSTITUTE ("UNK" Noun NC Plural) ("mío" Pronoun PPO Plural) TARGET ("<mis>") OR ("<Mis>");
SUBSTITUTE ("UNK" Noun NC Plural) ("nuestro" Pronoun PPO Plural) TARGET ("<nuestros>") OR ("<Nuestros>");
#LATEX T: Cambia lectura de "UNK" Noun NC Plural a "mío" Pronoun PPO Plural a "mis".
#LATEX E: \emph{mis,Mis} 
#LATEX D: Lo hacemos con nuestros (y quizás habrá que añadir otros posesivos).

SUBSTITUTE ("dar|decir" Verb VLfin Pres Command Sing 2nd) ("dar" Verb VLfin Pret Indi Sing 1st) TARGET DiLiteral IF (-1 PronombresCliticosTODOS);
#LATEX T: Cambia lectura de "di" de lema ambiguo entre dar y decir a sólo lema dar.
#LATEX E: \emph{le di, me di cuenta} 
#LATEX D: Dado que ``di'' como mandato llevará siempre el clítico pospuesto, si no lo lleva lo podemos considerar una forma de dar. Cabría tener en cuenta otras opciones pero esta parece razonable.

SUBSTITUTE (Verb VLinf Pres Command Plural 2nd) (Verb VLinf) TARGET ("<(.*)(ar|er|ir|ír)>"r); 
#LATEX T: Cambia lectura Command Plural 2nd a los infinitivos marcados como imperativo (y que estrictamente no los son)
#LATEX E: \emph{decir, nadar, temer} 
#LATEX D: Se aplica a cualquier infinitivo con la lectura arriba mencionada. A pesar de que los imperativos pueden tener esta función de mandatopragmáticamente hablando, consideramos que no se deben etiquetar morfológicamente como tales.
#LATEXREM Notad la ``r'' al final del TARGET en la regla. Indica que lo anterior debe ser tratado como una expresión regular. Si no, la regla no se aplica.

SUBSTITUTE ("UNK" Verb VLfin Pres Command Plural 2nd) ("platicar" Verb VLfin Pres Command Sing 3rd) TARGET ("<platíquenos>");
#LATEX T: Cambia lectura Command Plural 2nd y lema a ``platíquenos'' 
#LATEX E: \emph{platíquenos} 
#LATEX D: Se aplica sólo a ``platíquenos''.

SUBSTITUTE ("tener" Verb VMfin) ("tener" Verb VMfin Past Subj Plural 3rd) TARGET ("<tengan>");
#LATEX T: Añade lectura de pasado de subjuntivo a tener cuando este recibe el tag VMFin. 
#LATEX E: \emph{tengan} 
#LATEX D: Se aplica sólo a ``tengan''.

SUBSTITUTE ("para" Conjunction CSUBI) ("para" Preposition PREP) TARGET ("para");
#LATEX T: Cambia la lectura de conjunción de subordinación de infinitivas por la de preposición. 
#LATEX E: \emph{tengan} 
#LATEX D: Se aplica sólo a ``para'' como lema.
#LATEXREM Aunque es verdad que ``para'' puede ir seguida de un infinitivo para formar una subordinada infinitiva esta es una función que pueden desempeñar otras preposiciones, y creo que no por ello hay que cambiarles su categoría gramatical principal.

SUBSTITUTE ("crear" Verb VLfin Past Subj Plural 3rd) ("creer" Verb VLfin Pres Indi Plural 3rd) TARGET ("<creen>") IF (1 LoLiteral) (2 MismoLiteral) (3 Que);
SUBSTITUTE ("crear" Verb VLfin Past Subj Plural 3rd) ("creer" Verb VLfin Pres Indi Plural 3rd) TARGET ("<creen>") IF (1 En OR Que);
SUBSTITUTE ("crear" Verb VLfin Past Subj Plural 3rd) ("creer" Verb VLfin Pres Indi Plural 3rd) TARGET ("<creen>")  IF (-2 NoAdv) (-1 PronombresCliticosTODOS OR SeLiteral);
#LATEX T: Varias reglas para dar la lectura indicativa de ``creer'' a la forma ``creen'' en lugar de la subjuntiva de ``crear''. 
#LATEX E: \emph{tengan} 
#LATEX D: Se aplica teniendo en cuenta algunos contextos que generalmente decantaran la lectura hacia la interpretación prevista (creer).

SUBSTITUTE ("UNK" Noun NC Plural) ("bueno" Adjective ADJ Plural Masc) TARGET ("<Buenos>");
#SUBSTITUTE ("ojalá" Noun NP Singular) ("ojalá" Interjection Interj) TARGET ("para");

SUBSTITUTE ("cierta|cierto" Determiner QU) ("cierta|cierto" Determiner QU Plural Fem) TARGET ("<ciertas>") OR ("<Ciertas>");
SUBSTITUTE ("cierta|cierto" Determiner QU) ("cierta|cierto" Determiner QU Singular Fem) TARGET ("<cierta>") OR ("<Cierta>");
#LATEX T: Reglas para dar a cierta(s) el lema cierto. El lema cierta no se corresponde con ninguna lectura documentada en los diccionarios de referencia (RAE o DEM). 
#LATEX E: \emph{cierta}, \emph{cierta}
#LATEX D: NA.
##                               cierta|cierto                         12 ### LES CAMBIO EL LEMA A CIERTO

SUBSTITUTE ("cuarto" Determiner QU) ("cuarto" Noun NC Singular Masc) TARGET ("<cuarto>") OR ("<Cuarto>") IF (NOT 1 Nombre);
SUBSTITUTE ("cuarto" Determiner QU) ("cuarto" Adjective ORD Singular Masc) TARGET ("<cuarto>") OR ("<Cuarto>") IF (1C Nombre + SG);
#LATEX T: For some reason TreeTagger tags cuarto as DetQU. I assume this is because they include cardinals in this category, which is a very arguable decision in my opinion. I decided to change it to either noun or adjective depending on the contexts. 
#LATEX E: (...) nosotras teníamos que compartir nuestros, nuestro \emph{cuarto}, teníamos tres niñas por cuarto (...)  (Noun)
#LATEX E: (...) ellos eran, los paraguayitos eran de \emph{cuarto} grado. (Adjective)
#LATEX D: NA.

SUBSTITUTE ("doble" Determiner QU) ("doble" Adjective ADJ Singular Masc-Fem) TARGET ("<doble>") OR ("<Doble>");
#LATEX T: Contrary to TreeTagger RAE and most standard sources of Spanish linguistics think that doble is an adjective, even if it can be modifying a noun as some determiners can. For this, I decided to change its reading to adjective depending always. 
#LATEX E: (...) \emph{doble}
#LATEX D: NA.
##                               doble                                  4

SUBSTITUTE ("ambo" Determiner QU) ("ambos" Determiner QU Plural Masc) TARGET ("<ambos>") OR ("<Ambos>");
SUBSTITUTE ("ambo" Determiner QU) ("ambos" Determiner QU Plural Fem) TARGET ("<ambas>") OR ("<Ambas>");
#LATEX T: TreeTagger assignes lemma ambo to determiner ambos. The word ambo exists, but it has nothing to do (nowadays) with the determiner ambos/ambas. It will always be handled as a determiner. 
#LATEX E: (...) \emph{ambos}, \emph{ambas} ...
#LATEX D: NA.

SUBSTITUTE ("poco" Noun NC) ("poco" Adverb ADV) TARGET ("<poquito>") OR ("<POQUITO>") IF (NOT -1 UnDetLiteral);
SUBSTITUTE ("poco" Noun NC) ("poco" Adjective ADJ Singular Masc) TARGET ("<poquito>") OR ("<POQUITO>") IF (-1 UnDetLiteral);
#LATEX T: TreeTagger asigna la lectura nominal a poco en algunos contextos en que consideramos que debería ser adverbio o adjectivo. Estas reglas lo tratan.
#LATEX E: (...) \emph{ambos}, \emph{ambas} ...
#LATEX D: NA.


SUBSTITUTE ("y" Other ALFS) ("y" Conjunciton CC) TARGET ("<y>") OR ("<Y>");
SUBSTITUTE ("Y" Other ALFS) ("y" Conjunciton CC) TARGET ("<y>") OR ("<Y>");
SUBSTITUTE ("o" Other ALFS) ("o" Conjunciton CC) TARGET ("<o>") OR ("<O>");
SUBSTITUTE ("O" Other ALFS) ("o" Conjunciton CC) TARGET ("<o>") OR ("<O>");
SUBSTITUTE ("a" Other ALFS) ("a" Preposition PREP) TARGET ("<a>") OR ("<A>") (NOT 1 PalabraEnIngles);
#LATEX T: TreeTagger asigna la lectura ALFS a palabras como ``y'', ``o'' o ``a''. Estas reglas lo reparan.
#LATEX E: (...) \emph{y}, \emph{o}, \emph{a} ...
#LATEX D: Todavía quedan algunas letras sueltas que reciben esta etiqueta, pero no he seguido trabajando en ello porque son un número absoluto y relativo bastante bajo.

SUBSTITUTE ("medio" Preposition PDEL) ("medio" Noun NC Singular Masc) TARGET MedioLiteral IF (-1 PorLiteral) (1 PrepContract);
SUBSTITUTE ("medio" Adjective ADJ) ("medio" Determiner QU Singular Masc) TARGET MedioLiteral IF (1C Adj);
SUBSTITUTE ("medio" Noun NC Singular) ("medio" Determiner QU Singular Masc) TARGET MedioLiteral IF (0C MedioLiteral) (NOT 0 DetQU) (*-2 DetCardinales OR UnDetLiteral BARRIER FiniteVerbForms OR Conjuncion OR LimiteOracionSimple) (-1 YLiteral);
#LATEX T: Pasa de PDEL a NC a ``medio''. O de NC a QU, según los contextos.
#LATEX E: (...) \emph{un año y medio}, lo hice \emph{por medio de} su hermana ...
#LATEX D: TreeTagger asigna lecturas curiosas a ``medio''. Entre ellas PDEL en ``por medio de'' o nombre en expresiones como ``un año y medio''. Escribo un pare de reglas para cambiar esto, pero la verdad es que medio es una de esas palabras complicadas a la que cabría prestar atención (junto con el resto de determinantes y los llamados adjetivos pronominales, porque tiene un comportamiento peculiar).

SUBSTITUTE ("material" Adjective ADJ Plural) ("material" Noun NC Plural Masc) TARGET ("<materiales>"i) IF (-1 DetAllIncluded) (1 Adj + PL);
#LATEX T: Asigna lectura Noun a ``materiales'' en lugar de la adjetivo.
#LATEX E: (...) \emph{un año y medio}, lo hice \emph{por medio de} su hermana ...
#LATEX D: En contextos en que materiales va precedido por un determinante y seguido de un Adjetivo en plural.

SUBSTITUTE ("gracias" Preposition PREP) ("gracia" Noun NC Plural Fem) TARGET ("<gracias>"i);
#LATEX T: Asigna lectura Noun a ``gracias'' incluso si format parte de la locución ``gracias a''.
#LATEX E: \emph{gracias a} Dios, \emph{gracias a} la tecnología
#LATEX D: Que la locución ``gracias a'' sea prepositiva (o preposicional) no signigica que sus elementos sean todos preposiciones. Esto va contra las reglas de composición de expresiones (lexicalizadas o no) del lenguaje.

#LATEX\part{Secciones principales}

SECTION
#LATEXDOCU Las reglas de identificación de fenómenmos pedagógicos están ordenadas de más concretas a más generales. Así, por ejemplo, se detectan primero, cuando es posible, los distintos usos de \emph{para} (opinión, intención, destino, etc. como \emph{@Gram:Prep:Para:Opinion}...) y luego se marcan como simplemente \emph{@Gram:Prep:Para} el resto de ocurrencias de \emph{para}. Luego, se hace lo propio con otras preposiciones, hasta el punto que si para una preposición no hay reglas especiales, sus ocurrencias se marcan simplemente como \emph{@Gram:Prep}.

#LATEXDOCU Esta estrategia se utiliza para evitar que una misma palabra reciba por partida doble informaciones de igual granularidad. Para ello, antes de asignar una etiqueta, se comprueba que no haya otra asignada del mismo nivel. A efectos prácticos, etiquetas como \emph{@Gram:Prep:Para:Opinion}...) y \emph{@Gram:Prep:Para} se consideran del mismo nivel.

#LATEXDOCU Para evitar este tipo de duplicidades también se podría usar una estrategia basada en la distinción entre ADD y MAP (véase la documentación de vislcg3). Por ahora no se ha hecho, porque esto impide la asignación de múltiples etiquetas a un mismo token/una misma palabra.

#LATEXREM: Ahora mismo sólo estamos asignando hasta tres niveles de descripción pedagógica. Veáse el Google Doc Pedagogical tipologies \url{https://docs.google.com/spreadsheet/ccc?key=0AlyBsKogCRnwdDd3ZWRjejZlQU5XZWl6WVVUZmtKcEE&usp=sharing}.

#LATEX\chapter{Uso de los determinantes}
## SECTION -- RULES TO ADD Determiner-RELATED INFORMTATION
#LATEX\section{Usos de los artículos definidos}
#LATEXDOCU Identificaremos los usos de los artículos definidos \emph{el, la, los, las}, y el artículo (en algunas gramáticas considerado pronombre) \emph{lo} cuando va seguido de un adjetivo.

ADD (@Gram:Det:Art) Articulo IF (0 DetArtDefinidosLiterales) (NOT 1 FiniteVerbForms OR PronombreDeObjetoNoAmbiguos OR SeLiteral);
#LATEX T: Asigna @Gram:Det:Art
#LATEX E: \emph{el, la, los, las}
#LATEX D: Cualquier ocurrencia no seguida de un verbo finito o un pronombre clítico, porque treetagger no distingue entre su lectura como determinante y la pronominal.

ADDRELATION (Gram:Det:LoAdj) Articulo IF (0 LoLiteral) TO (*1 Adj BARRIER FiniteVerbForms OR Conjuncion OR LimiteOracionSimple LINK NOT 0 MejorLiteral) ;
ADDRELATION (Gram:Det:LoAdj) Articulo IF (0 LoLiteral) TO (1 Que) ;
ADDRELATION (Gram:Det:LoAdj) Articulo IF (0 LoLiteral) TO (*2 Que BARRIER FiniteVerbForms OR Conjuncion OR LimiteOracionSimple LINK -1 Adj + SG OR Adverbio OR DetQU + SG LINK NOT 0 LemasQueNoSonDetQUStandard) ;
#LATEX T: Asigna R:Gram:Det:Art
#LATEX E: \emph{lo} + ADJETIVO 
#LATEX E: \emph{lo} que 
#LATEX E: \emph{lo} poco/importante/mucho que 
#LATEX D: Cualquier ocurrencia de LO seguida de un adjetivo o de un que. Puede haber un adverbio intercalado. Quizás habrá que refinar la regla algún día. No anota ``a lo mejor'', ya que esta locución se usa más frecuentement en el sentido de quizás (por los menos en SPinTX).

#LATEX\section{Usos de los determinantes demostrativos}

ADD (@Gram:Det:Demo) ArtPronDemo IF (-1 Prep OR FiniteVerbForms) (0 EsteEseAquel) (NOT 0 EstoEsoAquello) (1 Nombre) ;
#LATEX T: Asigna @Gram:Det:Demo
#LATEX E: Prep/Verbo \emph{este/ese/aquel} (LEMMA) + Nombre
#LATEX D: Cualquier ocurrencia de un demostrativo precedida de una preposición o un verbo conjugado y seguido de un nombre.

#LATEX\section{Usos pronominales de los demostrativos}

ADD (@Gram:Det:Demo) ArtPronDemo IF (-1 Prep OR FiniteVerbForms) (0 EstoEsoAquello) ;
#LATEX T: Asigna @Gram:Pron:Demo
#LATEX E: Prep/Verbo \emph{esto/eso/aquello}
#LATEX D: Cualquier ocurrencia de un demostrativo terminado en -o. No distinguimos las formas que forman parte de locuciones (como ``por eso'', etc.).

#LATEX\section{Usos de los determinantes posesivos}

ADD (@Gram:Det:Pos) DetPosSing IF (*1 Nombre + SG BARRIER FiniteVerbForms OR Conjuncion OR LimiteOracionSimple) ;
ADD (@Gram:Det:Pos) DetPosPlu IF (*1 Nombre + PL BARRIER FiniteVerbForms OR Conjuncion OR LimiteOracionSimple) ;
ADD (@Gram:Det:Pos) DetPosFemPlu IF (*1 Nombre + PL BARRIER FiniteVerbForms OR Conjuncion OR LimiteOracionSimple) ;
ADD (@Gram:Det:Pos) DetPosMascPlu IF (*1 Nombre + PL BARRIER FiniteVerbForms OR Conjuncion OR LimiteOracionSimple) ;
ADD (@Gram:Det:Pos) DetPosFemSing IF (*1 Nombre + SG BARRIER FiniteVerbForms OR Conjuncion OR LimiteOracionSimple) ;
ADD (@Gram:Det:Pos) DetPosMascSing IF (*1 Nombre + SG BARRIER FiniteVerbForms OR Conjuncion OR LimiteOracionSimple) ;
#LATEX T: Asigna @Gram:Det:Poss
#LATEX E: Prep/Verbo \emph{mi/tu/su/sus/nuestro/nuestros/vuestro/vuestros} (LEMMA) + Nombre
#LATEX D: Cualquier ocurrencia de un determinante posesivo seguido de un nombre (se admiten ADV y ADJ intercalados).

#LATEX\section{Usos de los determinantes indefinidos}

ADD (@Gram:Det:CuantIndef) UnoLiteral IF (1 De) (NOT -1 Infinitive);
#LATEX T: Asigna @Gram:Det:CuantIndef
#LATEX E: Que no los puedo criticar porque yo soy \emph{uno} de ellos (...)
#LATEX D: Cualquier ocurrencia de un determinante posesivo seguido de un nombre (se admiten ADV y ADJ intercalados).

ADD (@Gram:Det:CuantIndef) DetQU IF (0 ListaDeDeterminantesSeguros) (NOT 0 NingunoLiteral);
#LATEX T: Asigna @Gram:Det:CuantIndef
#LATEX E: (...) porque él vivía aquí , en El Paso e iba \emph{cada} ocho días.
#LATEX D: Se aplica a cualquier ocurrencia de determinantes cuantitativos o indefinidos que consideramos ``seguros'' en el sentido de que no tienen intepretación adverbial: ``cualquier'', ``cada'', ``ambos'', ``alguno'', ``tal'' o ``ningún''.

ADD (@Gram:Det:CuantIndef) DetQU IF (0 ("<(.*)(os|as|a)>"r) + ListaDeDeterminantesSegurosSiNoEstanEnMasc) (NOT -1 DetAllIncluded);
#LATEX T: Asigna @Gram:Det:CuantIndef
#LATEX E: (...) éramos \emph{muchos} primos y fue muy una experiencia bonita.
#LATEX D: Se aplica a cualquier ocurrencia de determinantes cuantitativos o indefinidos que consideramos ``seguros'' en femenino, masculino plural o femenino plural. En masculino singular pueden tener lectura adverbial y por ello proponemos tratarlos a parte (con otro conjunto de reglas, pero con el mimsmo etiquetaje). Ejemplos: ``mucho'', ``poco'', ``tanto'', ``demasiado'', ``cierto'', ``varios'', y ``diversos''. A pesar de que TreeTagger etiqueta  ``dicho'' como determinante en algunos contextos, nosotros lo corregimos a posteriori y lo etiquetamos siempre como participio.
##                               pocos                                 93

ADD (@Gram:Det:Pron) DetQU IF (0 AlgoLema) (NOT 1 Adj OR AdvIntensificadorAdjetivos);
ADD (@Gram:Det:Pron) DetQU IF (0 NadaLema) (NOT 1 Adj OR AdvIntensificadorAdjetivos);
ADD (@Gram:Det:Pron) DetQU IF (0 AlgoLema OR NadaLema) (1C Adj);
#LATEX T: Asigna @Gram:Det:Pron
#LATEX E: 
#LATEX D: Se aplica a varios usos de ``nada'' y ``algo'', que demuestran que tiene un comportamiento esencialmente pronominal, porque nunca pueden aparecer modificando a un sustantivo.

ADDRELATION (Gram:Det:Combi) (Determiner) IF (0 DetAllIncluded) TO (1 ListaDeDeterminantesSegurosSiNoEstanEnMasc OR DemasLiteral OR OtroLema LINK NOT 0 Adverbio);
#LATEX T: Asigna @Gram:Det:Combi
#LATEX E: (...) \emph{las demás} mamás que estaban fuera, eran como si fueran nuestras propias madres.
#LATEX D: Se aplica sobre combinaciones de determinantes
##                               demás                                32
##                               pocos                                 93
##                               otro                                 341

ADDRELATION (Gram:Det:Combi) (Determiner) IF (0 TodosYTodasLiteral OR TodoYTodaLiteral) TO (1 DetAllIncluded);
#LATEX T: Asigna @Gram:Det:Combi
#LATEX E: Mi papá mudaba todas las cosas, \emph{todos los} muebles los ponía en el barco (...)
#LATEX D: Se aplica sobre combinaciones de determinantes
## (none)                        todo                                 640
## todos los, todas mis, todos aquellos, etc.


ADD (@Gram:Det:TodoEsp) (Determiner) IF (0 Todo) (NOT 1 DetAllIncludedNoPosPlural OR Prep OR FiniteVerbForms OR Adverbio OR LimiteOracionSimple OR Puntuacion OR SeLiteral);
#LATEX T: Asigna @Gram:Det:TodoEsp
#LATEX E: 
#LATEX D: No se aplica si va seguido de determinante, verbo conjugado o preposición porque entonces se trata de otro tipo de uso (seguramente el pronominal, como en ``se quebró todo'' o el pre-especificador, como en ``se comió todo el arroz'')

## PR
## (none)                        todo                                 640
## estábamos todos y no faltaba nadie
##                               algo                                 266
##                               nada                                 229


#LATEX\chapter{Género gramatical y artículos}

#LATEX\section{Género masculino en palabras terminadas en ``a''}
ADDRELATION (Gram:Genero:ExcepMasc) DetAllIncluded IF (0 ElDetLiteral + SG OR UnDetLiteral + SG OR ArtPronDemo + SG) TO (1 SustMascAcabadosEnA + Nombre + SG) ;
##ADDRELATION (Gram:Genero:ExcepMasc) DetAllIncluded IF (0 ElDetLiteral + SG) TO (1 SustMascAcabadosEnA + Nombre + SG) ;
#LATEX T: Asigna R:Gram:Genero:ExcepMasc
#LATEX E: \emph{el/un/ese} + (SUST acabado en -a, \emph{tema, día, problema, trauma})
#LATEX D: No se aplica con todos los determinantes porque TreeTagger no asigna género a todos y lematiza con criterios curiosos.

#LATEX\section{Cambio de artículo femenino a masculina en palabras con ``a'' tónica inicial}
ADDRELATION (Gram:Genero:ExcepArtFem) Articulo IF (0 ElDetLiteral) TO (1 SustFemConATonicaInicial + Nombre + SG) ;
#LATEX T: Asigna R:Gram:Genero:ExcepArtFem
#LATEX E: \emph{el} + (SUST con a- tónica inicial, \emph{arma, aula, agua})
#LATEX D: No se aplica con todos los determinantes porque TreeTagger no asigna género a todos y lematiza con criterios curiosos.


#LATEX\chapter{Uso de los adjetivos}
## SECTION -- RULES TO ADD Adj-RELATED INFORMTATION

ADD (Gram:Adj:Ord) AdjOrd IF (0 AdjOrd) (NOT 0 LiteralPrimeroOrTercero + SG) (-1 DetAllIncluded + SG OR Prep OR PrepContract OR Verbo) (1 Sustantivo + SG);
ADD (Gram:Adj:Ord) AdjOrd IF (0 AdjOrd) (NOT 0 LiteralPrimeroOrTercero + PL) (-1 DetAllIncluded + PL OR Prep OR Verbo) (1 Sustantivo + PL); 
#LATEX T: Asigna Gram:Adj:Ord
#LATEX E: \emph{por primera vez, del tercer mundo} 
#LATEX D: Se controla que el elemento de la izquierda sea un determinante, preposición, con o sin contracción, o verbo. El de la derecha un sustantivo y, cuando hay número, deben concordar. PrepContract no tiene sentido en la versión plural porque no se hace contracción entre preposición y artículos en plural.

#LATEX\chapter{Uso de las preposiciones}
## SECTION -- RULES TO ADD Prep-RELATED INFORMTATION

#LATEX\section{Usos de para}
#LATEXDOCU Veáse \url{http://dev.coerll.utexas.edu/spanishtx/spe/html/adv11.html}.

#LATEX\subsection{El uso de \emph{para} para expresar el destinatario o audiencia de una acción}
##ADD (@Gram:Prep:Para:Destinatario) Prep IF (-1 SerOServir + Verb) (0 Para) (NOT 0 GramPrep) (1 DetAll) (2 Personas);
ADD (@Gram:Prep:Para) Prep IF (-1 SerOServir + Verbo) (0 Para) (NOT 0 GramPrep) (1 DetAllIncluded) (2 Personas);
#LATEX T: Asigna @Gram:Prep:Para
#LATEX E: Fue escrito \emph{para} los estudiantes.
#LATEX E: Es fundamental \emph{para} una persona.
#LATEX D: Requiere \emph{para + DET + NOMBRE $<+humano>$}

##ADD (@Gram:Prep:Para:Destinatario) Prep IF (-1 SerOServir + Verb) (0 Para) (NOT 0 GramPrep) (1 Personas);
ADD (@Gram:Prep:Para) Prep IF (-1 SerOServir + Verbo) (0 Para) (NOT 0 GramPrep) (1 Personas);
#LATEX T: Asigna @Gram:Prep:Para.
#LATEX E: Ropa \emph{para} niños.
#LATEX D: Requiere \emph{para + NOMBRE $<+humano>$}

#LATEX\subsection{El uso de \emph{para} para expresar una finalidad}
## ADD (@Gram:Prep:Para:Finalidad) Prep IF (-1 BuenoOMalo) (0 Para) (NOT 0 GramPrep) (1 Infinitive);
#LATEXREM Dudo de si el uso de ``Juan es muy bueno para el fútbol'' se parece o no al de ``hemos venido antes para no quedarnos sin asiento''. De momento los pongo juntos.

###ADD (@Gram:Prep:Para:Finalidad) Prep IF (0 Para) (NOT 0 GramPrep) (1 Infinitive);
ADD (@Gram:Prep:Para) Prep IF (0 Para) (NOT 0 GramPrep) (1 Infinitive);
#LATEX T: Asigna @Gram:Prep:Para.
#LATEX E: Soy muy mala \emph{para} recordar.
#LATEX D: Requiere \emph{para + INFINITIVO}

## ADD (@Gram:Prep:Para:Finalidad) Prep IF (0 Para) (NOT 0 GramPrep) (1 PronPersFuerte OR No) (2 Infinitive);
ADD (@Gram:Prep:Para) Prep IF (0 Para) (NOT 0 GramPrep) (1 PronPersFuerte OR No) (2 Infinitive);
#LATEX T: Asigna @Gram:Prep:Para.
#LATEX E: Había que saber hablar inglés \emph{para} uno desarrollar su trabajo.
#LATEX D: Requiere \emph{para + PRONOMBRE PERSONAL NOMINATIVO + INFINITIVO}

## ADD (@Gram:Prep:Para:Finalidad) Prep IF (0 Para) (NOT 0 GramPrep) (1 Que);
ADD (@Gram:Prep:Para) Prep IF (0 Para) (NOT 0 GramPrep) (1 Que);
#LATEX T: Asigna @Gram:Prep:Para.
#LATEX E: Me daba clases \emph{para} que aprendiera inglés.
#LATEX D: Requiere \emph{para + QUE}

#LATEX\subsection{El uso de \emph{para} para expresar una opinión}
## ADD (@Gram:Prep:Para) Prep IF (0 Para) (NOT 0 GramPrep) (1 PronombrePersonal) (NOT 1 YoLiteral OR TuLiteral);
ADD (@Gram:Prep:Para) Prep IF (0 Para) (NOT 0 GramPrep) (1 PronombrePersonal) (NOT 1 YoLiteral OR TuLiteral);
#LATEX T: Asigna @Gram:Prep:Para.
#LATEX E: \emph{Para} mi.
#LATEX D: Requiere \emph{para + PRONOMBRE PERSONAL ACUSATIVO}
#LATEX C: Esta regla es muy floja. Habrá que revisarla.

#LATEX\subsection{Usos de \emph{para} no clasificados}
ADD (@Gram:Prep:Para) Prep IF (0 Para) (NOT 0 GramPrep);
#LATEX T: Asigna @Gram:Prep:Para.
#LATEX E: \emph{para} (cualquier otra ocurrencia)
#LATEX D: Requiere \emph{para}

#LATEX\section{Usos de \emph{por}}
#LATEXDOCU Veáse \url{http://dev.coerll.utexas.edu/spanishtx/spe/html/adv11.html}.

#LATEX\subsection{Usos de \emph{por} en locuciones}
##ADD (@Gram:Prep:Por:Locuciones) Prep IF (0 Por) (NOT 0 GramPrep) (1 LemasDePalabrasEnLocucionesConPor OR FormasDePalabrasEnLocucionesConPor);
ADD (@Gram:Prep:Por) Prep IF (0 Por) (NOT 0 GramPrep) (1 LemasDePalabrasEnLocucionesConPor OR FormasDePalabrasEnLocucionesConPor);
#LATEX T: Asigna @Gram:Prep:Por
#LATEX E: \emph{por ejemplo}, \emph{por eso}, \emph{por tanto}
#LATEX D: Requiere \emph{por} seguida por alguna palabra en la lista de palabras predefinidas \emph{LemasDePalabrasEnLocucionesConPor}

##ADD (@Gram:Prep:Por:Locuciones) Prep IF (0 Por) (NOT 0 GramPrep) (1 LoLiteral) (2 LemasDePalabrasEnLocucionesConPor);
ADD (@Gram:Prep:Por) Prep IF (0 Por) (NOT 0 GramPrep) (1 LoLiteral) (2 LemasDePalabrasEnLocucionesConPor);
ADD (@Gram:Prep:Por) (CSUBF) IF (0 Por) (NOT 0 GramPrep) (1 LoLiteral) (2 LemasDePalabrasEnLocucionesConPor);
#LATEX T: Asigna @Gram:Prep:Por.
#LATEX E: \emph{por lo tanto}, \emph{por lo regular}
#LATEX D: Requiere \emph{por} seguida por LO y alguna palabra en la lista de palabras predefinidas \emph{LemasDePalabrasEnLocucionesConPor}

#LATEX\subsection{Usos de \emph{por} no clasificados}
ADD (@Gram:Prep:Por) Prep IF (0 Por) (NOT 0 GramPrep);
#LATEX T: Asigna @Gram:Prep:Por.
#LATEX E: \emph{por} (cualquier ocurrencia)
#LATEX D: Requiere \emph{por}

#LATEX\subsection{Usos de preposiciones no clasificados}
ADD (@Gram:Prep) Prep (NOT 0 GramPrep);
#LATEX T: Asigna @Gram:Prep.
#LATEX E: \emph{PREP} (cualquier ocurrencia)
#LATEX D: Requiere \emph{PREP}

#LATEX\chapter{Uso de los pronombres}
## SECTION -- RULES TO ADD Prep-RELATED INFORMTATION

#LATEX\section{Pronombres de sujeto: los pronombres de sujeto personales}

ADD (@Gram:Pron:Sujeto) PronPersFuerte;
#LATEX T: Asigna @Gram:Pron:Sujeto.
#LATEX E: \emph{yo, tú, él, ella, ud., nosotros, vosotros, ellos, ellas, uds.} (cualquier ocurrencia)
#LATEX E: Cuando aquí en los Estados Unidos \emph{uno creció} con las lavadoras, ¿verdad? 
#LATEX D: La ocurrencia de uno de ellos.

#LATEX\subsection{Pronombres de sujeto: \emph{uno} como sujeto impersonal}

ADD (@Gram:Pron:Sujeto) UnoLiteral IF (1 FiniteVerbForms) ;
ADD (@Gram:Pron:Sujeto) UnoLiteral IF (-2 NoAdv OR PronombresCliticosTODOS OR Adverbio) (-1 Verbo);
ADD (@Gram:Pron:Sujeto) UnoLiteral IF (*1 Adverbio BARRIER GrupoNominal OR LimiteOracionSimple LINK 1 FiniteVerbForms) ;
ADD (@Gram:Pron:Sujeto) UnoLiteral IF (*1 Adverbio BARRIER GrupoNominal OR LimiteOracionSimple LINK 1 SeLiteral LINK 1 FiniteVerbForms) ;
#LATEX T: Asigna @Gram:Pron:Sujeto.
#LATEX E: Cuando aquí en los Estados Unidos \emph{uno creció} con las lavadoras, ¿verdad? 
#LATEX E: (...) \emph{uno} ya sabe si hablan el español
#LATEX D: La ocurrencia de uno de ellos. En cuanto a ``uno'', sólo se aplica si va seguido de un verbo conjugado, o si va precedido del adverbio ``no'' o un clítico y un verbo (conjugado o no).

#LATEX\section{Pronombres de objeto directo o complemento atributivo}
ADD (@Gram:Pron:Objeto) ElLema IF (NOT -1 Prep) (0 Articulo) (1 FiniteVerbForms) ;
#LATEX T: Asigna @Gram:Pron:Objeto
#LATEX E: mí nunca me \emph{la} hicieron pero que estabamos
#LATEX D: \emph{lo, la, los, las} VERBO CONJUGADO

#LATEX\section{Pronombres de objeto directo, indirecto, o posesivos}
ADD (@Gram:Pron:Objeto) PronPers IF (0 PronombreDeObjetoNoAmbiguos);
#LATEX T: Asigna @Gram:Pron:Objeto
#LATEX E: \emph{me, te, os, nos} (cualquier ocurrencia)
#LATEX D: La ocurrencia de uno de ellos.

#LATEX\section{Pronombres de objeto indirecto}
ADD (@Gram:Pron:Objeto) PronPersClitico IF (0 LeLema);
#LATEX T: Asigna @Gram:Pron:Objeto
#LATEX E: \emph{le, les}
#LATEX D: La ocurrencia de uno de ellos.

#LATEX\section{Pronombres de objeto doble}
ADDRELATION (Gram:Pron:ObjetoDoble) PronPers (0 PronombreDeObjetoNoAmbiguos) TO (1 ElLema + Articulo);
ADDRELATION (Gram:Pron:ObjetoDoble) PronPersSE (0 SeLiteral) TO (1 ElLema + Articulo);
#LATEX T: Asigna @Gram:Pron:ObjetoDoble.
#LATEX E: \emph{se lo, se la, se los, se las, me lo, me la, me los, me las, te lo, te la, te los, te las,} etc.
#LATEX D: La ocurrencia de cualquier combinación de dos pronombres clíticos.

#LATEX\section{Pronombre se (solo)}
ADD (@Gram:Pron:Otros) PronPersSE (0 SeLiteral) (NOT 0 GramPron) (NOT 1 FiniteVerbForms);
#LATEX T: Asigna @Gram:Pron:Otros.
#LATEX E: \emph{se lo, se la, se los, se las, me lo, me la, me los, me las, te lo, te la, te los, te las,} etc.
#LATEX D: La ocurrencia de cualquier combinación de dos pronombres clíticos.

## SECTION -- RULES TO ADD PAST TENSE RELATED INFORMTATION
#LATEX\chapter{Formas verbales conjugadas}

#LATEX\section{Formas verbales para tiempos de indicativo}

#LATEX\subsection{Pretérito imperfecto}
# PRETÉRTIO IMPERFECTO
ADD (@Gram:Imperf) FiniteVerbForms IF (0 FiniteVerbForms + PretImperfSimple) (NOT 0 Haber) ;
#LATEX T: Asigna @Gram:Imperf.
#LATEX E: \emph{cantaba, corría, temía, partía} (cualquier ocurrencia)
#LATEX D: Requiere una forma identificada como pretérito simple que no sea del verbo haber (auxiliar).

#LATEX\subsection{Pretérito perfecto}

#LATEX\subsubsection{Pretérito perfecto simple}
# PRETÉRTIO PERFECTO SIMPLE
ADD (@Gram:Indefinido) FiniteVerbForms IF (0 FiniteVerbForms + PretPerfSimple) (NOT 0 Haber) ;
#LATEX T: Asigna @Gram:PretPerf.
#LATEX E: \emph{cantó, corrió, anduvo, partió} (cualquier ocurrencia)
#LATEX D: Requiere una forma identificada como pretérito perfecto simple que no sea del verbo haber (auxiliar).

#LATEX\subsubsection{Pretérito perfecto perifrástico o compuesto}\label{sec:HaberAuxPretPerfecto}
# PRETÉRTIO PERFECTO PERIFRÁSTICO
ADDRELATION (Gram:PerfPerif Gram:Aux:Haber) FiniteVerbForms (0 Haber + FiniteVerbForms + Presente) (NOT 0 Subjuntivo) TO (1 Part);
#LATEX T: Asigna R:Gram:PerfPerif a la forma del verbo HABER
#LATEX E: \emph{ha cantado, ha corrido, ha andado dormido} (cualquier ocurrencia)
#LATEX D: Requiere una forma finita de HABER seguida de un participio

#LATEX\subsection{Futuro: con o sin perífrasis}
### FITA JUL 9
ADD (@Gram:VFut:Conjugado) FiniteVerbForms IF (0 FiniteVerbForms + Futuro);
#LATEX T: Asigna @Gram:VFut:Conjugado a la forma del verbo
#LATEX E: A veces íbamos en bicicleta en el sexto grado me \emph{compré} una moto de Estados Unidos (...)
#LATEX D: Cualquier forma de un verbo inambiguamente conjugado en futuro.

ADDRELATION (Gram:VFut:Perifrasis) FiniteVerbForms IF (0 LemaIr) (1 APrep) TO (2 Verbo + Infinitive)
#LATEX T: Asigna RGram:VFut:Perifrasis a la locución.
#LATEX E: (...) porque siempre \emph{va a haber} gente que ... que se te \emph{va a meter} en el camino
#LATEX D: Cualquier forma de un verbo inambiguamente conjugado en futuro.

#LATEX\section{Verbos piscológicos: gustar, encantar, sorprender y otros}
#LATEXDOCU Veáse \url{http://dev.coerll.utexas.edu/spanishtx/spe/html/adv13.html}.
#LATEXDOCU Veáse \url{http://dev.coerll.utexas.edu/spanishtx/spe/html/beg05.html}.

#LATEX\subsection{Usos de \emph{gustar} para expresar gustos propios}
ADDRELATION (Gram:Gustar:Propios) PronPers (0 Me OR NosLiteral) TO (1 Gustar-Like + 3aPersAmbig);
#LATEX T: Asigna @Gram:Gustar:Propios.
#LATEX E: \emph{me gusta} ayudar a mis amigos 
#LATEX D: Requiere la forma ME seguida de una forma de GUSTAR en tercera persona (posiblemente ambigua)

ADDRELATION (Gram:Gustar:Propios) PronPers (0 Me OR NosLiteral) (1 Haber) TO (1 Gustar-Like + Part);
#LATEX T: Asigna R:Gram:Gustar: Propios a la forma del verbo HABER
#LATEX E: \emph{me ha gustado} ayudar a mis amigos 
#LATEX E: \emph{me hubieras gustado} con un vestido verde 
#LATEX D: Requiere la forma ME seguida de una forma de HABER en tercera persona (posiblemente ambigua)

#LATEX\subsection{Usos de \emph{gustar} para expresar gustos de tu interlocutor}
ADD (@Gram:Gustar:Interlocutor) FiniteVerbForms IF  (-1 Tu) (0 Gustar-Like + 3aPersAmbig);
#LATEX T: @Gram:Gustar:Interlocutor
#LATEX E: Dime, \emph{te gusta} vivir en Estados Unidos.] 
#LATEX D: Secuencia del lema TE y un forma de GUSTAR en tercera persona del singular.

#LATEX\subsection{Usos de \emph{gustar} para expresar gustos de otras personas}
ADDRELATION (Gram:Gustar:Ajenos) LeLema (0 LeLema) TO (1 Gustar-Like + 3aPersAmbig);
#LATEX T: R:Gram:Gustar:Ajenos a la forma del verbo GUSTAR
#LATEX E: Dime, \emph{le gusta} vivir en Estados Unidosa tu hijo.
#LATEX D: Secuencia del lema LE y un forma de GUSTAR en tercera persona del singular.

#LATEX\section{Expresar sorpresa}
#LATEXDOCU Veáse \url{http://dev.coerll.utexas.edu/spanishtx/spe/html/adv13.html}.
#LATEX\subsection{Usos de \emph{sorprender}}

ADDRELATION (Gram:Gustar:Otros) Yo (0 Yo) TO (1 Sorprender-Like + 3aPersAmbig);
ADDRELATION (Gram:Gustar:Otros) Yo (0 Yo) To (*1 Sorprender-Like BARRIER GrupoNominal OR LimiteOracionSimple LINK -1 Haber + 3aPersAmbig);
#LATEX T: Asigna Gram:Gustar:Otros (Gram:ExpresarSorpresas:Propias) a la forma de SORPRENDER
#LATEX E: Siempre \emph{me sorprende} su forma de hablar.
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Gram:Gustar:Otros) Tu (0 Tu) To (1 Sorprender-Like + 3aPersAmbig);
#LATEX T: Asigna Gram:Gustar:Otros (antes era Gram:ExpresarSorpresas:Interlocutor) a la forma de SORPRENDER
#LATEX E: ¿Y \emph{te sorprende} su forma de hablar?
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

#ADDRELATION (Gram:Gustar:Otros) Part (0 Sorprender-Like) TO (*-1 Tu BARRIER GrupoNominal OR LimiteOracionSimple LINK 1 Haber + 3aPersAmbig);
ADDRELATION (Gram:Gustar:Otros) Tu (0 Tu) To (*1 Sorprender-Like BARRIER GrupoNominal OR LimiteOracionSimple LINK -1 Haber + 3aPersAmbig);
#LATEX T: Asigna Gram:Gustar:Otros (antes era Gram:ExpresarSorpresas:Interlocutor) a la forma de HABER
#LATEX E: ¿Hay alguna palabra mexicana que \emph{te haya sorprendido}?
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Gram:Gustar:Otros) LeLema (0 LeLema) To (1 Sorprender-Like + 3aPersAmbig);
#LATEX T: Asigna Gram:Gustar:Otros (antes era Gram:ExpresarSorpresas:Ajenas) a la forma de SORPRENDER
#LATEX E: ¿Y \emph{le sorprende} su forma de hablar?
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Gram:Gustar:Otros) LeLema (0 LeLema) TO (*1 Sorprender-Like BARRIER GrupoNominal OR LimiteOracionSimple LINK -1 Haber + 3aPersAmbig);
#LATEX T: Asigna Gram:Gustar:Otros (antes era Gram:ExpresarSorpresas:Ajenas) a la forma de HABER
#LATEX E: ¿Hay alguna palabra mexicana que \emph{le haya sorprendido}?
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

#LATEX\section{Formas verbales para tiempos de subjuntivo}
ADD (@Gram:MSub:Pres) Verbo IF (0 Presente + Subjuntivo) (NOT -1 De) (NOT 1 Part);
ADD (@Gram:MSub:Pret) FiniteVerbForms IF (0 Verbo + Pasado + Subjuntivo) (NOT -1 De) (NOT 1 Part);
ADDRELATION (Gram:MSub:Pres) FiniteVerbForms (0 Haber + Verbo + Presente + Subjuntivo) (NOT -1 De) TO (1 Part);
ADDRELATION (Gram:MSub:Pret) FiniteVerbForms (0 Haber + Verbo + Pasado + Subjuntivo) (NOT -1 De) TO (1 Part);
#LATEX T: Asigna Gram:MSub:Pres a verbos en presente o pretérito de subjuntivo
#LATEX E: ¿Hay alguna palabra mexicana que \emph{le haya sorprendido}?
#LATEX D: No se aplica si la forma verbal no esta precedida por una "de" para evitar formas incorrectamente marcadas como subjuntivo por el tagging básico. Si se trata de una forma compuesta, es decir, HABER + PART, se marca todo como tal.

#LATEX\section{Formas verbales para tiempos de imperativo}

#LATEX\subsection{Formas verbales para tiempos de imperativo informal (tú)}
ADD (@Gram:MImp:Informal) Verbo IF (0 Presente + Imperativo + SG + 2aPers) (NOT 0 ("<órale>") OR ("<(.*)(é|í|á)rselo>"r));
###Verb VLfin Pres Command Sing 2nd 
#LATEX T: @Gram:MImp:Informal
#LATEX E: \emph{cuéntame}... 
#LATEX D: Cualquier forma de imperativo de la segunda persona del singular. No se aplicará en formas como cantárselo, comérselo o partírselo, pero sí en otras formas infinitivas con pronombres de objeto que TreeTagger etiquete como imperativos.
#LATEXREM Actualmente falla en ``díael'' (debido a un error de transcripción). Se ha arreglado el problema de transcripción y en próximas versiones no debería aparecer.

#LATEX\subsection{Formas verbales para tiempos de imperativo formal (usted)}
ADD (@Gram:MImp:Formal) Verbo IF (0 Presente + Imperativo + SG + 3aPers);
#LATEXREM Sólo aparecen tres ocurrencias (detectadas) de uso del imperativo en la forma formal.

#LATEX\subsection{Formas verbales para tiempos de imperativo en negativo (no mires!)}

#LATEXDOCU No hemos anotado usos de imperativo en negativo (p.e., ¡No compre(s) un vino tan caro!) ni en el registro formal ni el informal.

## SECTION -- RULES TO ADD PAST GUSTAR AND SIMILAR VERBS
#LATEX\section{Verbos copulativos}

#LATEX\subsection{Verbo ser}

ADD (@Gram:SerEstar:Ser) Verbo (0 VerboSer OR VerboSerAmbiguoConErar) (NOT -1 Haber);
ADD (@Gram:SerEstar:Ser) Verbo (0 VerboSerAmbiguoConIr) (NOT 1 Prep) (NOT -1 Haber);
ADDRELATION (Gram:SerEstar:Ser) Verbo (0 Haber + FiniteVerbForms) TO (1 VerboSer + Part OR VerboSerAmbiguoConErar + Part) (NOT 1 Part);
#LATEX T: @Gram:SerEstar:Ser
#LATEX E: \emph{soy, eres,} \emph{ha sido,}, pero no \emph{ha sido comprado}etc. 
#LATEX E: \emph{fui, fuimos...} NOT Prep 
#LATEX D: Cualquier forma de \emph{ser} del pretérito indefinido no seguido de una preposición (porque entonces seguramente será del verbo ir). También las formas perifrásticas con haber que no vayan seguidas de un participicio.

#LATEX\subsection{Verbo estar}
ADD (@Gram:SerEstar:Estar) Verbo (0 VerboEstar);
#LATEX T: @Gram:SerEstar:Estar
#LATEX E: \emph{estoy, estás, estaban,} etc. 
#LATEX D: Cualquier forma de \emph{estar}

#LATEX\section{Verbo haber}
#LATEX\subsection{Verbo haber en su uso com existencial}
ADD (@Gram:Haber:Exist) Verbo (0 Haber) (NOT 1 Part);
#LATEX T: @Gram:Haber:Exist
#LATEX E: \emph{Había} tres personas en el ascensor. 
#LATEX D: Cualquier forma de \emph{haber} que no vaya seguida de un participio

#LATEX\section{Verbos auxiliares: ser, estar y haber}

#LATEX\subsection{Verbo estar en su uso como auxiliar}
#LATEXDOCU Se etiqueta con RGram:SerEstar:EstarAux las secuencias de estar seguido de un gerundio.
ADDRELATION (Gram:SerEstar:EstarAux) VerboEstar IF (0 FiniteVerbForms) TO (1 Gerundio);
ADDRELATION (Gram:SerEstar:EstarAux) VerboEstar IF (0 Infinitive) TO (1 Gerundio);
#LATEX T: R:Gram:SerEstar:EstarAux
#LATEX E: y me \emph{estuvo platicando} allí
#LATEX D: Actualmente sólo se aplica sobre formas conjugadas y en secuencias estar (infinitivo) más gerundio. Habrá que ver cuán necesario es aplicar reglas para "hemos estado mirando" o "estando comprando?" (el interrogante los uso para marcar que dudo de la gramaticalidad de esta oración.

#LATEX\subsection{Verbo haber en su uso como auxiliar en tiempos verbales compuestos}
#LATEXDOCU Se etiqueta con @Gram:Aux:Haber, pero se hace en las reglas que sirven para etiquetar los tiempos verbales correspondientes. Véase por ejemplo la anotación (y la regla) del Presente Perfecto en la sección \ref{sec:HaberAuxPretPerfecto}. 

#LATEX\subsection{Verbo ser en su uso como auxiliar}

#LATEXDOCU No se etiquetan las ocurrencias de ser como auxiliar en las construcciones pasivas por dos razones: una es que hay muy pocas ocurrencias (unas 10 o 12) y otra es que se confunden a menudo con usos adjetivales del participio (por ejemplo, \emph{soy callada y reservada}, \emph{la otra era licenciada en administración de empresas}, etc. Entre unos y otros había unos 31 casos en el Batch 1 (los primeros 327 clips publicados). Véase la Figura \ref{fig:InstancesOfSerPlusParticiple}.

#LATEXDOCU \begin{figure}[htb]
#LATEXDOCU \centering
#LATEXDOCU \includegraphics[scale=0.5]{Figs/InstancesOfSerPlusParticiple.png}
#LATEXDOCU \caption{Ocurrencias de ser seguido de participio en Batch 1.}
#LATEXDOCU \label{fig:InstancesOfSerPlusParticiple}
#LATEXDOCU \end{figure}

## SECTION -- RULES TO ADD PAST GUSTAR AND SIMILAR VERBS
#LATEX\chapter{Funciones comunicativas}

#LATEX\section{Expresar deseos con incertidumbre/en el futuro}

#LATEXDOCU Esta sección contiene reglas que sirven para detectar estructuras lingüísticas generalmente utilizadas para expresar deseos que implican a uno mismo o a terceros. Esto hace, a menudo y según en qué construcciones, que sea necesario el subjuntivo. Así, por ejemplo, diremos ``me gustaría que te casaras conmigo'' para hacerle entender a nuestro interlocutor lo que deseamos que realice, pero en cambio diremos ``me gustaría casarme conmigo'' cuando queremos expresar que es nuestro deseo el que se realice. El primero usa subjuntivo y el segundo no.

#LATEX\subsection{Expresar deseos con incertidumbre/en el futuro con agentes distintos en principal y subordinada}

    # ESPERAR/PEDIR + QUE + (SUJETO) + (CLITICO) + SUBJUNTIVO
    # ESPERAR + (SUJETO) + QUE + (CLITICO) + SUBJUNTIVO
    # PEDIR/SUGERIR/RECOMENDAR (CLITICO) A X QUE + SUBJUNTIVO

ADDRELATION (Func:Deseos:Ajenos) Verbo IF (0 VerbosExpresarDeseos) (1 Que) TO (*2 Verbo + Subjuntivo BARRIER GrupoNominal OR LimiteOracionSimple OR FiniteVerbForms);
#LATEX T: R:Func:Deseos:Ajenos
#LATEX E: (...) me sorprendió mi mamá porque no \emph{esperaba que me regalaran} nada (...)
#LATEX D: Permite la presencia de clíticos entre el ``que'' y el subjuntivo que le sigue.
    # 55916:  años que pues sí me sorprendió mi mamá porque no <esperaba> que me regalaran nada y llegó una bicicleta que nomás
    # 51375:  porque cuando ellas empezaron a hablar inglés yo siempre les <pedía> que me hablaran en inglés porque yo quería aprenderlo .
    # 45168:  Yanesa tenía dos años , me contestó , yo le <pedí> que hiciera algo y ella me dijo algo así como
    # 43121:  estábamos en Rusia , él y otros colaboradores cristianos les <pidieron> que fueran a ... a la Universidad de Moscú porque
    # This rule adds 8 new examples of this function

ADDRELATION (Func:Deseos:Ajenos) Verbo IF (0 VerbosExpresarDeseos) (1 Adverbio OR MejorLiteral LINK 1 Que) TO (*3 Verbo + Subjuntivo BARRIER GrupoNominal OR LimiteOracionSimple OR FiniteVerbForms);
#LATEX T: R:Func:Deseos:Ajenos
#LATEX E: (...) me  \emph{gustaría mejor que te casaras} conmigo (...)
#LATEX D: Permite la presencia de adverbios entre el verbo de la principal y el ``que''.
    # 35273:  no ? pues este ... a mí no me gustaría que me dejaras , me <gustaría mejor que te casaras> conmigo
    # 52257:  ella se quedó viviendo todavía allí , en el San Francisco del Oro , y <me gusta mucho que> me esté contando y 

ADDRELATION (Func:Deseos:Ajenos) Verbo IF (0 VerbosExpresarDeseos) (1 Que) (2 DetAllIncluded) (3 Sustantivo) (4 PronombresCliticosTODOS OR SeLiteral) TO (*5 Verbo + Subjuntivo BARRIER GrupoNominal OR LimiteOracionSimple OR FiniteVerbForms);
#LATEX T: R:Func:Deseos:Ajenos
#LATEX E: (...) \emph{espero que el español se vuelva} tal vez otro idioma oficial (...)
#LATEX D: Permite la presencia de un sujeto entre el ``que'' y el subjuntivo.
    # 67630:  de América Latina . Creo que ... el español ... <espero> que el español se vuelva tal vez otro idioma oficial
 
ADDRELATION (Func:Deseos:Ajenos) OjalaLiteral IF (0 OjalaLiteral) (1 Que) (2 DetAllIncluded) (3 Sustantivo) TO (*4 Verbo + Subjuntivo BARRIER GrupoNominal OR LimiteOracionSimple OR FiniteVerbForms);
#LATEX T: R:Func:Deseos:Ajenos
#LATEX E: Este ... \emph{ojalá que algún día cambie} , ¿ no ? Pero . ¿ Y tú opinas
#LATEX D: Permite la presencia de un sintagma nominal sujeto entre el ``que'' y el subjuntivo.

ADDRELATION (Func:Deseos:Ajenos) Verbo IF (0 VerbosExpresarDeseos) (1 DetAllIncluded) (2 Sustantivo OR Adj) (3 Que) (4 PronombresCliticosTODOS OR SeLiteral) TO (*5 Verbo + Subjuntivo BARRIER GrupoNominal OR LimiteOracionSimple OR FiniteVerbForms);
#LATEX T: R:Func:Deseos:Ajenos
#LATEX E: (...) no \emph{esperas tu niña que se comporte} así (...)
#LATEX D: Permite un alteración sintáctica entre el sujeto de la subordinada y la posición del ``que''. Esto es algo inusual en la lengua escrita, pero dejamos la regla por si en el futuro permite detectar construcciones similares en la lengua oral.
    ## ESPERAR
    # 42140:  a querer ser el Llanero Solitario , digo , no <esperas> tu niña que se comporte así , y esta niña

    # ## LA NECESIDAD DE QUE X SUBJUNTIVO

ADDRELATION (Func:Deseos:Ajenos) Sustantivo IF (0 NombresExpresarDeseos) (1 De OR Para) (2 Que) TO (*3 Verbo + Subjuntivo BARRIER LimiteOracionSimple OR FiniteVerbForms LINK T:SintagmaNominal-Y-O-Clitico);
#LATEX T: R:Func:Deseos:Ajenos
#LATEX E: (...) sin \emph{necesidad de que me tengan} que pagar.
#LATEX D: Marca la ocurrencia de nombres como ``necesidad, petición'' seguidos de un ``para'' o un ``que'' y un verbo subjuntivo, y permite que aparezca un clítico o un sintagma nominal (Det N, Det Adj) entre el ``que'' y el verbo en subjuntivo.
#LATEXREM: Esta regla utiliza el operador ``T:''. Esta ``T'' indica que los que sigue a los dos puntos (:) es un TEMPLATE definido más arriba, en la zona de definición de LIST y SET. Para más detalles véase la documentación vislcg3 y el grupo de debate (GoogleGroup).

## Las dos reglas que siguen están sólo para mostrar que son equivalentes a la anterior.
## ADDRELATION (Func:Deseos:Ajenos) Sustantivo IF (0 NombresExpresarDeseos) (1 De OR Para) (2 Que) (3 DetAllIncluded) (4 Sustantivo OR Adj) TO (*5 Verbo + Subjuntivo BARRIER GrupoNominal OR LimiteOracionSimple OR FiniteVerbForms);
## ADDRELATION (Func:Deseos:Ajenos) Sustantivo IF (0 NombresExpresarDeseos) (1 De OR Para) (2 Que) (3 PronombresCliticosTODOS OR SeLiteral) TO (*4 Verbo + Subjuntivo BARRIER GrupoNominal OR LimiteOracionSimple OR FiniteVerbForms);

	 # 78110:  algo , les muestro algo , completamente gratis , sin <necesidad de que> me tengan que pagar . Muy bien , y platíqueme

#LATEX\subsection{Expresar recomendaciones y sugerencias}
ADDRELATION (Func:Sugerencias) Verbo IF (0 VerbosExpresarRecomendaciones) (1 Que) TO (*2 Verbo + Subjuntivo BARRIER LimiteOracionSimple OR FiniteVerbForms);
#LATEX T: Asigna la etiqueta R:Func:Sugerencias
#LATEX E: el médico les dice que él \emph{sugiere que apaguen} la máquina de  
#LATEX D: NA.

    # ## SUGERIR
    # 41625:  el ... el ... el médico les dice que él <sugiere> que apaguen la máquina de ... de incubadora porque mi
    
ADDRELATION (Func:Sugerencias) Verbo IF (0 VerbosExpresarRecomendaciones + 2aPers OR VerbosExpresarRecomendaciones + 3aPers) TO (1 Verbo + Infinitive);
#LATEX T: Asigna la etiqueta R:Func:Sugerencias
#LATEX E: mis amigos también fueron muy conscientes de que \emph{necesitaban hablar} un poquito más lento cuando estaban conmigo 
#LATEX D: NA.

   ##  81738:  compañeros de escuela y mis amigos también fueron muy conscientes de que <necesitaban hablar> un poquito más lento cuando estaban conmigo ... ahora , cuando me
   ##  84720:  que aquí tú necesitas algo y vas a la tienda , allá <necesitabas esperar> haber cuando encontrabas lo que podías encontrar , entonces había mercados en
   ##  23624:  o algo , pero yo , honestamente , creo que uno siempre <necesita saber> de donde uno viene , y no perder eso . Si uno
   ##  89450:  y que no se va a ir , que ahí está , <necesitas resolver> tus problemas . Entonces , yo llevo dieciséis años de casada caminando

## Expresando necesidades de uno mismo
   ##  86193:  sintiera mal mami . Y es todo , es todo lo que <necesitaba hacer> con Yanesa , el sólo hecho de que me estés haciendo sentir
   ## 143445:  quisieron convalidar mis estudios cuando yo llegué acá a Estados Unidos y <necesitaba ahorrar> dinero para ... para estudiar en el college , comencé a trabajar
   ##  99671:  lo que nos enseñaron en escuela fue la ... las cosas que <necesitábamos saber> sobre Estados Unidos , sobre Tejas . Las cosas que necesitábamos que
   ## 139003:  no tanto por aprender o por tener una carrera , sino porque <necesitaba conocer> todos los aspectos de la cultura de aquí y básicamente descubrí que

   ##  27818:  y puedo comunicar mis ideas , y lo voy a hacer si <necesito hacerlo> . Y bueno , siendo el ... el ... esta , esta
   ##  82289:  me doy cuenta de que es una señora y la señora dice <necesito hablar> con Olga Alexeuna , Olga Alexeuna , Olga Alexeuna , ¿ está
   ##  82362:  , pero no puedes decirlo y la persona se empezó a enojar <necesito hablar> con Olga Alexeuna , pásele el teléfono a Olga Alexeuna , ¿

ADDRELATION (Func:Sugerencias) Verbo IF (0 Ser + Verbo) (*1 AdjetivosUsadosEnRecomendaciones BARRIER LimiteOracionSimple OR FiniteVerbForms OR GrupoNominal) (2 Que) TO (*3 Verbo + Subjuntivo BARRIER LimiteOracionSimple OR FiniteVerbForms);
#LATEX T: Asigna la etiqueta R:Func:Sugerencias
#LATEX E: Usted cree que va a \emph{ser importante} que le enseñen a sus nietos los dos idiomas 
#LATEX D: NA.
  ## Usted cree que va a <ser importante> que le enseñen a sus nietos los dos idiomas

ADDRELATION (Func:Sugerencias) Verbo IF (0 Ser + Verbo) (*1 AdjetivosUsadosEnRecomendaciones BARRIER LimiteOracionSimple OR FiniteVerbForms OR GrupoNominal) TO (*2 Verbo + Infinitive BARRIER LimiteOracionSimple OR FiniteVerbForms LINK NOT 0 Ser);
#LATEX T: Asigna la etiqueta R:Func:Sugerencias
#LATEX E: Ahorita mucha gente lo sabe hablar y \emph{es importante saber} comunicarte con otra gente
#LATEX D: NA.
  ## Ahorita mucha gente lo sabe hablar y <es importante> saber comunicarte con otra gente
  ## por qué <era bueno> salir adelante

### OTROS USOS DE NECESITAR -- SOME DAY

#LATEXREM: Existen otros usos de necesitar, más parecidos a requerir como por ejemple (1)  [...] sería muy , muy interesante , pero por ejemplo para ese trabajo ``necesitas ser'' ciudadano , así que es ... es muy importante o (2) [...] yo estoy más que dispuesta a trabajar con usted nada más ``necesito estar'' en casa cuando AISD no tiene clases. Ambas oraciones muestran un uso que no está claro que sea un consejo o una recomendación, especialmente la primera. Ambas son necesidades, requerimientos externos o personales.

#LATEX\section{Expresar objetivos y finalidades con ``para''}
ADDRELATION (Func:Finalidades) Prep IF (0 Para) (1 Verbo + Infinitive) TO (*2 Que BARRIER LimiteOracionSimple OR FiniteVerbForms LINK 1 Verbo + Subjuntivo);
#LATEX T: R:Func:Finalidades
#LATEX E: (...) Yo crecí pensando que para orar o \emph{para pedirle a Dios que hiciera algo} (...)
#LATEX D: NA.
    # Yo crecí pensando que para orar o para <pedirle a Dios> que hiciera algo o para confesar mis pecados

#LATEX\subsection{Estilo indirecto}

    # ## DECIR USADO COMO PEDIR (o simplemente estilo indirecto de una órden)
    # 76646:  marcas , yo no tenía idea que querían entonces tenía <que decirles que me señalaran> cuál marca querían . En Colombia hay tres o cuatro


    # ## ME GUSTAR QUE SUBJUNTIVO (problemas con repeticiones oralidad)
    # me gustaría <que ... que siguieran> siendo como es uno
    
#LATEX\subsection{Expresando opiniones}

    # ## ES (MUY) ADJ QUE SUBJUNTIVO
    # 9657:  hacer los ... para que hagamos . , yo digo <que es muy bueno que sepan> l0s dos idiomas . ¿ Dónde trabajas ? En K-



#LATEX\section{Expresar opiniones, dudas}

    # ## ES (MUY) ADJ QUE SUBJUNTIVO
    # 32585:  ? ... no pues como te digo era muy raro <que supieran , que fuera> a 

    # ## NO CREER QUE XXX
    # no creo que esté bien <que se burlen 

#LATEX\section{Expresar necesidades}

ADDRELATION (Func:Necesidades) Verbo (0 NecesitarLema) TO (**1 SustantivosQueDenotanNecesidades + Nombre BARRIER LimiteOracionSimple OR FiniteVerbForms);
#LATEX T: R:Func:Necesidades
#LATEX E: si \emph{necesitan ayuda} en algo
#LATEX D: NA

## ADDRELATION (Func:Necesidades) Verbo (0 NecesitarLema) (NOT 0 YaAnotadoComoNecesidad) (NOT -1 Que LINK -1 LoLiteral) TO (1 Infinitive);

ADDRELATION (Func:Necesidades) LoLiteral (0 LoLiteral) (NOT 0 YaAnotadoComoNecesidad) (1 Que) TO (2 NecesitarLema + Verbo); 
#LATEX T: R:Func:Necesidades
#LATEX E: \emph{lo que necesitaba} yo para
#LATEX D: NA

ADDRELATION (Func:Necesidades) TuLiteral (0 TuLiteral) (NOT 0 YaAnotadoComoNecesidad) (1 QueConAcento) TO (2 NecesitarLema + Verbo); 
#LATEX T: R:Func:Necesidades
#LATEX E: \emph{tú qué necesitas}, muchachita
#LATEX D: NA

ADDRELATION (Func:Necesidades) Verbo (0 NecesitarLema) (NOT 0 YaAnotadoComoNecesidad) (1 APrep) TO (*2 DetAllIncluded OR NombrePropio OR Dios BARRIER LimiteOracionSimple OR FiniteVerbForms LINK NOT 0 Articulo);  
#LATEX T: R:Func:Necesidades
#LATEX E: \emph{necesito a Jill} en mi vida.
#LATEX D: NA

ADDRELATION (Func:Necesidades) Verbo (0 NecesitarLema) (NOT 0 YaAnotadoComoNecesidad) (1 APrep) TO (*2 DetAllIncluded BARRIER LimiteOracionSimple OR FiniteVerbForms OR Prep LINK 1 Sustantivo);
#LATEX T: R:Func:Necesidades
#LATEX E: \emph{necesito a un profesor} de inglés.
#LATEX D: NA

ADDRELATION (Func:Necesidades) Verbo (0 NecesitarLema) (NOT 0 YaAnotadoComoNecesidad) TO (*1 DetAllIncluded BARRIER LimiteOracionSimple OR FiniteVerbForms OR Prep LINK 1 Sustantivo); 
#LATEX T: R:Func:Necesidades
#LATEX E: ellos \emph{necesitan las persona} a su lado y
#LATEX E: \emph{necesitas resolver tus problemas}.
#LATEX D: NA

ADDRELATION (Func:Necesidades) Verbo (0 Ser) TO (**1 NecesarioLema BARRIER LimiteOracionSimple OR FiniteVerbForms OR Prep); 
#LATEX T: R:Func:Necesidades
#LATEX E: (no) \emph{es algo muy necesario}
#LATEX D: NA

ADDRELATION (Func:Necesidades) (Determiner) (0 DetAllIncluded) (1 Sustantivo) TO (2 NecesarioLema);
#LATEX T: R:Func:Necesidades
#LATEX E: \emph{los materiales necesarios}
#LATEX D: NA
 
ADDRELATION (Func:Agradecer) (Determiner) (0 MuchoLema) (1 Gracia + PL) TO (2 Por);
#LATEX T: R:Func:Agradecer
#LATEX E: \emph{muchas gracias por}
#LATEX D: NA

ADDRELATION (Func:Agradecer) (Determiner) (0 MuchoLema) (1 Gracia + PL) (2 PuntuacionComa) TO (3 NombrePropio);
#LATEX T: R:Func:Agradecer
#LATEX E: \emph{muchas gracias, Sabrina}
#LATEX D: NA

ADDRELATION (Func:Agradecer) Nombre (0 Gracia + PL) (1 APrep) TO (2 NombrePropio OR PronPersFuerte OR Dios);
#LATEX T: R:Func:Agradecer
#LATEX E: \emph{gracias a Dios}, \emph{gracias a él}
#LATEX D: NA

ADDRELATION (Func:Agradecer) Nombre (0 Gracia + PL) TO (1 APrep LINK T:SintagmaNominalHaciaLaIzda);
#LATEX T: R:Func:Agradecer
#LATEX E: \emph{gracias a la tecnología}
#LATEX D: NA

ADDRELATION (Func:Agradecer) Verbo (0 DarLema + Verbo) TO (**1 Gracia + PL BARRIER LimiteOracionSimple OR FiniteVerbForms OR Prep);
#LATEX T: R:Func:Agradecer
#LATEX E: \emph{damos las gracias}, \emph{darle gracias}
#LATEX D: NA

ADDRELATION (Func:Agradecer) LeLema (0 LeLema) (1 AgradecerLema + Verbo) TO (1 MuchoLema);
#LATEX T: R:Func:Agradecer
#LATEX E: \emph{le agradezco mucho} a ella
#LATEX D: NA

ADDRELATION (Func:Agradecer) PronPersFuerte (0 PronPersFuerte) (1 AgradecerLema + Verbo) TO (2 ArtPronDemo);
ADDRELATION (Func:Agradecer) Verbo (0 AgradecerLema + Gerundio) TO (**2 Que BARRIER LimiteOracionSimple OR FiniteVerbForms OR Prep LINK -1 LoLiteral);
#LATEX T: R:Func:Agradecer
#LATEX E: \emph{yo agradezco eso}
#LATEX E: \emph{agradeciendo lo que tienes}
#LATEX D: NA

#LATEX\section{Expresar objetivos (con para que)}
    # Tú hiciste/Indi un esfuerzo para <que tus hijos continuaran/Subj> hablando el español 
    # para <que él tuviera que venderlas>
    # 78186:  se lo pagaba con botellas de agua ardiente , para <que él tuviera que venderlas> , para poder traer el dinero a la casa y

#LATEXREM Oración sacada del corpus. 45388:  que viene a vivir a ? . ¿ Qué le <recomendaría> a un hispano que viene a vivir a o a (...) . Esta frase anterior no es la misma construcción (que la de los deseos con sjubjuntivo) porque el que ``viene a vivir'' es una adjetival de hispano, no una subordinada de recomendar.

#LATEXREM  Oración sacada del corpus. 45410:  Estados Unidos ? , más como yo . Yo le <recomendaría> , en cuanto al idioma , trate de aprenderlo lo (...) Esta oración refleja la omisión del que (oral o literario, algo recargado). Este tipo de estructuras requerirían un análisis lingüístico más sofisticado, seguramente -- sintáctico, análisis de incisos, etc.

	   # TO DO -- SOME DAY
    # ## PETICIONES SIN SUBJUNTIVO
    # 57966:  Conforme estaba en la preparatoria empecé con la maestra le <pedí> que si me daba trabajo en su despacho y gracias


#LATEX\section{Expresar deseos y recomendaciones (implicando a uno mismo)}

##TODO

	    
   # ## ESPERAR

   # 62107:  , muy buenos hijos , no he tenido problemas , <espero> no tenerlos en el futuro . Y creo que han
   # ## NECESITAR
   # 41470:  puedes decirlo y la persona se empezó a enojar , <necesito> hablar con Olga Alexina , pásele el teléfono a Olga
   # 41528:  ha dado una risa porque le dije a mi esposo <necesito> que me escribas las posibles respuestas a posibles llamadas pero

   # 45230:  mal mami y es todo , es todo lo que <necesitaba> hacer con Yanesa , el sólo hecho de que me
   # 45999:  y yo estoy dispuesta a trabajar con usted nada más <necesito> estar en casa cuando AISD no tiene clases . Enton
   #        no tanto por tener una carrera sino porque <necesitaba> conocer todos los aspectos de la cultura de aquí y
   # 78972:  o sea ya había estudiando antes , entonces como que <necesitaba> ... practicarlo más y poco a poco incluso ahora lo
   # 79651:  mis estudios cuando yo llegué acá a Estados Unidos y <necesitaba> ahorrar dinero para ... para estudiar en el , comencé
   # 80215:  , sería muy interesante pero por ejemplo para ese trabajo <necesitas> ser ciudadano , así que es ... es muy im

## SECTION -- RULES TO ADD PAST GUSTAR AND SIMILAR VERBS
#LATEX\chapter{Pragmática}

#LATEX\section{Marcadores discursivos}
#LATEXDOCU Véase \url{http://www.cs.famaf.unc.edu.ar/~laura/shallowdisc4summ/index.html}.

#LATEX\subsection{Marcadores discursivos indicadores de revisión}
## Prag:MarcadoresDisc:Revision

ADD (@Prag:MarcadoresDisc:Revision) MarcDiscRevision;
#LATEX T: @:Prag:MarcadoresDisc:Revision
#LATEX E: \emph{aunque}, \emph{excepto} o \emph{pero}
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]
# aunque
# excepto
# pero

ADDRELATION (Prag:MarcadoresDisc:Revision) Sin TO (1 Embargo);
#LATEX T: R:Prag:MarcadoresDisc:Revision (relación entre el primer i el último elemento de la locución)
#LATEX E: \emph{sin embargo}
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Prag:MarcadoresDisc:Revision) De TO (1 Hecho);
#LATEX T: R:Prag:MarcadoresDisc:Revision (relación entre el primer i el último elemento de la locución)
#LATEX E: \emph{de hecho}
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Prag:MarcadoresDisc:Revision) En TO (1 Realidad);
#LATEX T: R:Prag:MarcadoresDisc:Revision (relación entre el primer i el último elemento de la locución)
#LATEX E: \emph{en realidad}
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

# 2 tokens (NO TRATADOS)
# pese a
# salvo que
#ADDRELATION (Prag:MarcadoresDisc:Revision) No (1 Obstante);
# no obstante
# al contrario
# con todo
# ahora bien

# 3 tokens
ADDRELATION (Prag:MarcadoresDisc:Revision) APrep (1 Pesar) TO (2 De);
#LATEX T: R:Prag:MarcadoresDisc:Revision (relación entre el primer i el último elemento de la locución)
#LATEX E: \emph{a pesar de}
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Prag:MarcadoresDisc:Revision) De (1 Todos) TO (2 Modos);
#LATEX T: R:Prag:MarcadoresDisc:Revision (relación entre el primer i el último elemento de la locución)
#LATEX E: \emph{de todos modos}
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Prag:MarcadoresDisc:Revision) Es (1 Cierto) TO (2 Que);
#LATEX T: R:Prag:MarcadoresDisc:Revision (relación entre el primer i el último elemento de la locución)
#LATEX E: \emph{s cierto que}
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

# 4 tokens (NO TRATADOS)
# el hecho es que

#LATEX\subsection{Marcadores discursivos indicadores de causa}

ADD (@Prag:MarcadoresDisc:Causa) Porque;
#LATEX T: @Prag:MarcadoresDisc:Causa
#\LATEX E: (...) y es curioso \emph{porque} hace más o menos una semana que lo vi (...)
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Prag:MarcadoresDisc:Causa) Prep IF (0 Por) (1 LoLiteral) TO (2 Tanto);
ADDRELATION (Prag:MarcadoresDisc:Causa) Prep IF (0 Por) TO (2 Tanto OR FormasDePalabrasEnLocucionesCausalesConPor);
#LATEX T: Asigna @Prag:MarcadoresDisc:Causa.
#LATEX E: \emph{por lo tanto}
#LATEX E: \emph{por eso}
#LATEX D: Requiere \emph{por} seguida por LO y TANTO

ADDRELATION (Prag:MarcadoresDisc:Causa) Debido TO (1 APrep);
#LATEX T: R:Prag:MarcadoresDisc:Causa (relación entre el primer i el último elemento de la locución)
#\LATEX E: (...) mi propio negocio pues \emph{debido a} unas dificultades (...)
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

ADDRELATION (Prag:MarcadoresDisc:Causa) Nombre (0 Gracia + PL) TO (1 APrep);
#LATEX T: R:Prag:MarcadoresDisc:Causa (relación entre el primer i el último elemento de la locución)
#\LATEX E: \emph{gracias a} la tecnología este sí conservo amistades (...)
#LATEX D: [Espacio para describir con palabras lo que hace la regla.]

###ADDRELATION (Prag:MarcadoresDisc:Causa) Gracia (0 PL) (1 APrep) TO (2 Dios);
###LATEX T: R:Prag:MarcadoresDisc:Causa (relación entre el primer i el último elemento de la locución)
###LATEX E: y de ahí fui a la ceremonia y \emph{gracias a dios} me hice ciudadano
###LATEX D: [Espacio para describir con palabras lo que hace la regla.]

#LATEX\subsection{Marcadores discursivos indicadores de igualdad}
## Prag:MarcadoresDisc:Igualdad

ADD (@Prag:MarcadoresDisc:Igualdad) Adverbio IF (0 Ademas) (NOT 1 De);
ADD (@Prag:MarcadoresDisc:Igualdad) Adverbio IF (0 Tambien);
#LATEXREM No añado \emph{precisamente} porque es ambiguo entre su lectura literal (preciso, justo, cierto) como modificador verbal y su lectura como marcador discursivo. Para anotarlo con fiabilidad habría que tener en cuenta su posición respecto al principio de la oración y el verbo principal. Ejemplos: \emph{sí muchas pero no sé precisamente cuál cuál sería una}, \emph{allí vivimos como un año y precisamente yo me tomé me tomaron fotos en un un estudio de fotografía}
#LATEX T: @:Prag:MarcadoresDisc:Igualdad
#LATEX E: \emph{además}, \emph{también}
#LATEX D: Requiere \emph{además} o \emph{también} no seguido de DE

ADDRELATION (Prag:MarcadoresDisc:Igualdad) Prep IF (0 Por) TO (1 Ejemplo);
#LATEX T: R:Prag:MarcadoresDisc:Igualdad (relación entre el primer i el último elemento de la locución)
#LATEX E: \emph{por ejemplo}
#LATEX D: Requiere \emph{por} seguida por alguna palabra en la lista de palabras predefinidas \emph{LemasDePalabrasEnLocucionesConPor}

ADDRELATION (Prag:MarcadoresDisc:Igualdad) ConjLetraO TO (1 Sea);
#LATEX T: R:Prag:MarcadoresDisc:Igualdad
#LATEX E: \emph{o sea}
#LATEX D: NA.

ADDRELATION (Prag:MarcadoresDisc:Igualdad) No TO (1 SoloAcento);
#LATEX T: R:Prag:MarcadoresDisc:Igualdad
#LATEX E: porque uno \emph{no sólo} puede ayudar en el aspecto de inglés pero en el aspecto del español también
#LATEX D: NA.

ADD (@Prag:MarcadoresDisc:Probabilidad) Adverbio IF (0 QuizasConOSinEse);
#LATEX T: R:Prag:MarcadoresDisc:Probabilidad
#LATEX E: (...) lo que sí me gustaría es que \emph{quizás} no tuvieran un acento muy marcado (...)
#LATEX D: NA.

ADDRELATION (Prag:MarcadoresDisc:Probabilidad) Tal TO (1 Vez);
#LATEX T: R:Prag:MarcadoresDisc:Probabilidad
#LATEX E: noto que \emph{tal vez} sus hijos hablan el spanglish más unos más que otros
#LATEX D: NA.

ADDRELATION (Prag:MarcadoresDisc:Probabilidad) APrep (1 LoLiteral) TO (2 MejorLiteral);
#LATEX T: R:Prag:MarcadoresDisc:Probabilidad
#LATEX E: Pero, \emph{a lo mejor} era no más porque estaba chiquita (...)
#LATEX D: NA.

#LATEX\subsection{Elementos discursivos usadors para ganar tiempo}
## Prag:MarcadoresDisc:Revision

ADD (@Prag:RecuDiscurso:GanarTiempo) ArtPronDemo IF (NOT -1 Prep) (0 EsteLiteral) (NOT 1 Nombre);
#LATEX T: @Prag:ElementosDiscurso:GanarTiempo
#LATEX E: \emph{Este} en mi niñez era un poco más soñadora mucho más soñadora de hecho (...)
#LATEX D: Anota ocurrencies de ESTE que no vayan precedidas de una preposición ni seguidas de un sustantivo.

## SECTION -- RULES TO ADD PAST GUSTAR AND SIMILAR VERBS
#LATEX\chapter{Estructuras características del habla en hablantes de herencia}

#LATEX\section{Estructuras derivadas del contacto entre lenguas}
## Herit:Contacto:Mixtas

#LATEX\subsection{Expresiones con cambio de código}
#LATEXDOCU Incluímos aquí expresiones que mezclan palabras en ambas lenguas, en este caso el español y el inglés. En la bibliografía especializada se utiliza el término inglés ``code switching''.

## TO DO: NEEDS DIFFERENT INPUT TO WORK
ADDRELATION (Herit:Contacto:Mixtas) Verbo IF (0 ListaVerbosCorpusParaContactoMixtos) TO (*1 PalabraEnIngles BARRIER PalabraEnEspanol);
#LATEX T: R:Herit:Contacto:Mixtas
#LATEX E: \emph{hacer request}, \emph{comer turkey}
#LATEX D: Anota secuencias de un verbo seguid de una o más palabras en inglés. Por ahora lo limitamos a ser, hacer, decir y comer porque TreeTagger anota como verbo algunas palabras que no lo son y no queremos generar ruido para los usuarios.

ADDRELATION (Herit:Contacto:Mixtas) (Determiner) IF (0 DetAllIncluded) TO (**1 PalabraEnIngles BARRIER PalabraEnEspanol);

### FITA: no s'està mapejant la info de llengua i per tant la regla anterior no funciona
### 02/21/2013

#LATEX\section{Usos de inidicativo dónde se esperaría subjuntivo (en español estándard)}
#LATEXREM Sección en proceso de implementación.

## TODO
##    49950:  a mirar esas peleas , y ese día ella estaba <esperando> que iba a llegar la pelea , y al final
##    57410:  mí me gusta vivir lo que viene , no estar <esperando> que voy a hacer esto o hacer aquello . Se



#LATEX\chapter{Vocabulario multipalabra}
## Vocab

#LATEX\section{Nombres propios}

#LATEX\subsection{Nombres de localizaciones geográficas}
ADDRELATION (Vocab:Entities:Geo) Articulo IF  (0 ("<El>")) TO (1 ("<Paso>"));
ADDRELATION (Vocab:Entities:Geo) Nombre IF  (0 ("<Estados>")) TO (1 ("<Unidos>"));
ADDRELATION (Vocab:Entities:Geo) Nombre IF  (0 ("<Ciudad>")) TO (1 ("<Juárez>"));
ADDRELATION (Vocab:Entities:Geo) Nombre IF  (0 ("<Rancho>")) TO (1 ("<Viejo>"));
ADDRELATION (Vocab:Entities:Geo) Adj IF  (0 ("<Buenos>")) TO (1 ("<Aires>"));
ADDRELATION (Vocab:Entities:Geo) Nombre IF  (0 ("<Río>")) TO (1 ("<Grande>") LINK NOT 1 ("<City>")) ;
ADDRELATION (Vocab:Entities:Geo) Nombre IF  (0 ("<Río>")) TO (2 ("<City>") LINK -1 ("<Grande>") ) ;
#LATEX T: R:Vocab:Entities:Geo
#LATEX E: \emph{Ciudad Juárez}, \emph{El Paso}, \emph{Estados Unidos}  
#LATEX D: Reglas para anotar entidades frecuentes en el corpus. En este caso de nombres geográficos.

#LATEX\section{Verbos con regímenes especiales}

#LATEX\subsection{Combinaciones de verbo y preposición de régimen}
ADDRELATION (Vocab:Fraseol:VerbPrep) Verbo IF  (0 ("hablar")) TO (*1 Prep BARRIER LimiteOracionSimple OR GrupoNominal OR Verbo);
ADDRELATION (Vocab:Fraseol:VerbPrep) Verbo IF  (0 ("pensar")) TO (*1 Prep BARRIER LimiteOracionSimple OR GrupoNominal OR Verbo);
ADDRELATION (Vocab:Fraseol:VerbPrep) Verbo IF  (0 ("empezar")) TO (*1 Prep BARRIER LimiteOracionSimple OR GrupoNominal OR Verbo);
#LATEX T: R:Vocab:Fraseol:VerbPrep
#LATEX E: \emph{hablar PREP}, \emph{pensar PREP}, \emph{empezar PREP}  
#LATEX D: Ahora no se distingue entre usos lexicalizados y usos composicionales (p.e., ``empezar en San Antonio y terminar en Austin'' se trata igual que ``empezé con la tradición en la preparatoria'') 

#LATEX\section{Locuciones}

#LATEX\subsection{Locuciones adverbiales}

ADDRELATION (Vocab:Fraseol:LocAdv) PorLiteral IF  (0 PorLiteral) TO (*1 DeODel BARRIER LimiteOracionSimple OR GrupoNominal OR Verbo LINK -1 MedioLiteral);
# por medio de
#LATEX T: R:Vocab:Fraseol:LocAdv
#LATEX E: \emph{por medio de}  
#LATEX D: NA.

ADDRELATION (Vocab:Fraseol:LocAdv) UnDetLiteral TO (*1 PocoLema BARRIER LimiteOracionSimple OR GrupoNominal OR Verbo LINK 1C Adj OR Adverbio);
#LATEX T: R:Vocab:Fraseol:LocAdv
#LATEX E: \emph{un poco}  
#LATEX D: Se trata del uso de ``un poco'' como unidad léxica que modifica a adjetivos o adverbios.

ADDRELATION (Vocab:Fraseol:LocAdv) NadaLema TO (1 MasLiteral) (NOT 1 Adj);
#LATEX T: R:Vocab:Fraseol:LocAdv
#LATEX E: \emph{nada más}  
#LATEX D: Use de ``nada más'' en el sentido de sólo. Recuerda al ``nomás'' oral.

##AFTER-SECTIONS


#ADD (@Gram:Prep:Por:Locuciones) Prep IF (0 Por) (1 FormasFrecEnLocPor);


#ADDRELATION (Gram:PerfPerif) VerboConjugado (0 Haber) TO (1 Participio);


#ADD (@Gram:Det:Art) Articulo IF (0 Lo) (*1 Adj BARRIER NPBoundary);

#LATEX\part{Comentarios y observaciones}

#LATEX\chapter{Retos en la identificación de ``no''-palabras}
#LATEXDOC En ocasiones los hablantes bilingües utilizan palabras que no forma parte de ningún diccionario estándar (en un proceso muy parecido al que realizan los y las aprendices de una segunda lengua). Por ejemplo, en nuestro corpus tenemos a un hablante que dice ``trato de hablar mucho con mis primos me tartudeo (sic) poquito''. Claramente la palabra que quiere utilizar es ``tartajeo'' o ``tartamudeo'', pero le sale otra. Una búsqueda rápida en Google me confirma que algunos pocos hablantes de español usan esta mimsa palabra. En todo caso, es un reto identificar que hay un proceso creativo de formación verbal ``tartadear'' y, por tanto, ``tartadeo''. 

#LATEX\chapter{Retos en la identificación de usos semánticos}
#LATEXDOC Por escribir.
## SEMANTIC CHALLENGES
# distinguish necesitar, two meanings: you need a photo-ID vs. you need to go to the doctor (consejo vs necesidad)
# distinguish ir a + INF when it has future meaning or meaning de desplazamiento like in voy a comprar al mercado (check if more common order is voy al mercado a comprar)